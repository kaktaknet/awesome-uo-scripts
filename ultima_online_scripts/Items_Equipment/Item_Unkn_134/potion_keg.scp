[ITEMDEF i_keg_potion]
NAME=empty potion Keg
ID=i_KEG_WOOD
TYPE=t_script
VALUE={200 300}
WEIGHT=40
RESOURCES=8 i_BOARD, 1 i_BARREL_TAP, 1 i_BARREL_HOOPS
SKILLMAKE=TINKERING 65.0,t_tinker_tools
DYE=1

CATEGORY=Unique Items
SUBSECTION=Tinker
DESCRIPTION=empty potion Keg

ON=@CLICK
IF (<MORE1>==0)
RETURN 0
ELSE
MESSAGE <TAG.POT_NAME>
MESSAGE <eval <MORE1>> Doses
RETURN 1
ENDIF
	
ON=@DCLICK
TARGET Choose the potion or the empty bottle.
RETURN 1

ON=@CREATE
MORE1=0
TAG.POT_ID=0

ON=@TARGON_ITEM
IF (<SRC.ACTION> != -1 )
SRC.SYSMESSAGE You can't use this yet!
RETURN 1

ELSEIF !(<SRC.TARG.CONT>==<SRC.FINDLAYER.21.UID>)
SRC.SYSMESSAGE You cannot use this on items you do not possess.
RETURN 1

ELSEIF (<SRC.TARG.TYPE>==t_potion)
	IF (<MORE1>==100)
	SRC.SYSMESSAGE This keg is full.
	RETURN 1

	ELSEIF (<MORE1>==0)
	TAG.POT_ID=<SRC.TARG.BASEID>
	TAG.POT_NAME=<SRC.TARG.NAME>
		IF (<SRC.TARG.AMOUNT> >=2)
		SRC.TARG.AMOUNT=<SRC.TARG.AMOUNT>+(-1)
		SRC.TARG.UPDATEX
		ELSE
		SRC.TARG.REMOVE
		ENDIF
	SRC.NEWITEM=i_bottle_empty
	SRC.ACT.BOUNCE
	SOUND=snd_Z_END_LIQUID
	MORE1=<MORE1>+1
		IF (<SRC.TARG.DISPID>==i_bottle_black)
		COLOR=0455
		UPDATEX
		ELSEIF (<SRC.TARG.DISPID>==i_bottle_green)
		COLOR=044
		UPDATEX
		ELSEIF (<SRC.TARG.DISPID>==i_bottle_blue)
		COLOR=05d
		UPDATEX
		ELSEIF (<SRC.TARG.DISPID>==i_bottle_orange)
		COLOR=030
		UPDATEX
		ELSEIF (<SRC.TARG.DISPID>==i_bottle_white)
		COLOR=1154
		UPDATEX
		ELSEIF (<SRC.TARG.DISPID>==i_bottle_red)
		COLOR=027
		UPDATEX
		ELSEIF (<SRC.TARG.DISPID>==i_bottle_yellow)
		COLOR=036
		UPDATEX
		ELSEIF (<SRC.TARG.DISPID>==i_bottle_purple)
		COLOR=012
		UPDATEX
		ELSEIF (<SRC.TARG.DISPID>==i_bottle_empty)
		COLOR=<SRC.TARG.COLOR>
		UPDATEX
		ENDIF
	RETURN 1

	ELSEIF (<SRC.TARG.BASEID>==<TAG.POT_ID>)
		IF (<SRC.TARG.AMOUNT> >=2)
		SRC.TARG.AMOUNT=<SRC.TARG.AMOUNT>+(-1)
		SRC.TARG.UPDATEX
		ELSE
		SRC.TARG.REMOVE
		ENDIF
	SRC.NEWITEM=i_bottle_empty
	SRC.ACT.BOUNCE
	SOUND=snd_Z_END_LIQUID
	MORE1=<MORE1>+1
	RETURN 1

	ELSE
	SRC.SYSMESSAGE You cannot mix potions.
	RETURN 1
	ENDIF

ELSEIF (<SRC.TARG.BASEID>==i_bottle_empty)
	IF (<MORE1>==0)
	SRC.SYSMESSAGE This keg is empty!
	RETURN 1

	ELSEIF (<MORE1> >= 2)
		IF (<SRC.TARG.AMOUNT> >=2)
		SRC.TARG.AMOUNT=<SRC.TARG.AMOUNT>+(-1)
		SRC.TARG.UPDATEX
		SRC.NEWITEM=<TAG.POT_ID>
		SRC.ACT.BOUNCE
		SOUND=snd_Z_END_LIQUID
		MORE1=<MORE1>+(-1)
		RETURN 1

		ELSE
		SRC.TARG.REMOVE
		SRC.NEWITEM=<TAG.POT_ID>
		SRC.ACT.BOUNCE
		SOUND=snd_Z_END_LIQUID
		MORE1=<MORE1>+(-1)
		RETURN 1
		ENDIF

	ELSE
	SRC.TARG.REMOVE
	SRC.NEWITEM=<TAG.POT_ID>
	SRC.ACT.BOUNCE
	SOUND=snd_Z_END_LIQUID
	MORE1=0
	TAG.POT_ID=
	TAG.POT_NAME=
	COLOR=0
	UPDATEX
	SRC.SYSMESSAGE This keg is now empty
	RETURN 1
	ENDIF

ELSE
SRC.SYSMESSAGE Choose a potion or empty bottle!
RETURN 1
ENDIF


[EOF]