// Scripted by Exercisto

[PLEVEL 1]
C
GC

[PLEVEL 2]
SC

[FUNCTION C]
IF !(STRCMPI(<ARGS>,?)
SRC.SYSMESSAGE @0a5a  Explanation:
SRC.SYSMESSAGE @07ad  Type .C [message] to use this.
SRC.SYSMESSAGE
SRC.SYSMESSAGE @0a5a  Example:
SRC.SYSMESSAGE @07ad  .C Hi! I am <NAME>.
RETURN 1

ELIF !(STRCMPI(<ARGS>,ON)) && (<TAG0.CHAT>==0)
SRC.SYSMESSAGE @0a5a  Note:
SRC.SYSMESSAGE @07ad  Chat is now activated.
SRC.TAG.CHAT 1
RETURN 1

ELIF !(STRCMPI(<ARGS>,OFF)) && (<TAG0.CHAT>==1)
SRC.SYSMESSAGE @0a5a  Note:
SRC.SYSMESSAGE @07ad  Chat is now deactivated.
SRC.TAG.CHAT 0
RETURN 1

ELIF (<TAG0.CHAT>==0)
SRC.SYSMESSAGE @0a5a  Error:
SRC.SYSMESSAGE @07ad  Chat is not activated yet.
SRC.SYSMESSAGE @07ad  Type .C ON to activate Chat.
RETURN 1

ELIF (<ISEMPTY <ARGS>>)
SRC.SYSMESSAGE @0a5a  Error:
SRC.SYSMESSAGE @07ad  You did not type a message.
RETURN 1
ENDIF

SERV.ALLCLIENTS C_R [[Chat] <NAME> -> <ARGS>

[FUNCTION C_R]
IF (<TAG0.CHAT>==1)
SYSMESSAGE @07ad  <ARGS>
RETURN 1
ENDIF

[FUNCTION SC]
IF (<ISEMPTY <ARGS>>)
SRC.SYSMESSAGE @0a5a  Error:
SRC.SYSMESSAGE @07ad  You did not type a message.
RETURN 1
ENDIF

SERV.ALLCLIENTS SC_R [[Staff Chat] <NAME> -> <ARGS>

[FUNCTION SC_R]
IF (<ACCOUNT.PLEVEL>>1)
SYSMESSAGE @0a5a  <ARGS>
RETURN 1
ENDIF

[FUNCTION GC] 
IF (<ISEMPTY <ARGS>>)
SRC.SYSMESSAGE @0a5a  Error:
SRC.SYSMESSAGE @07ad  You did not type a message.
RETURN 1
ENDIF

IF (<MEMORYFINDTYPE.MEMORY_GUILD.LINK.UID>)
SERV.ALLCLIENTS GC_R [[Guild Chat] <NAME> <MEMORYFINDTYPE.MEMORY_GUILD.LINK.ABBREV> -> <ARGS> 
RETURN 1
ELSE
SRC.SYSMESSAGE @0a5a  Error:
SRC.SYSMESSAGE @07ad  You are not in a guild.
RETURN 1
ENDIF

[FUNCTION GC_R] 
IF <MEMORYFINDTYPE.MEMORY_GUILD.LINK.UID> == <SRC.MEMORYFINDTYPE.MEMORY_GUILD.LINK.UID>
SYSMESSAGE @0a5a  <ARGS>
ENDIF
 
[FUNCTION GPANIC] 
IF (<MEMORYFINDTYPE.MEMORY_GUILD.LINK.UID>)
SERV.ALLCLIENTS GPANIC_R <NAME><MEMORYFINDTYPE.MEMORY_GUILD.LINK.ABBREV> needs help at <REGION.NAME>!
ELSE
SRC.SYSMESSAGE @0a5a  Error:
SRC.SYSMESSAGE @07ad  You are not in a guild.
RETURN 1
ENDIF
 
[FUNCTION GPANIC_R] 
IF <MEMORYFINDTYPE.MEMORY_GUILD.LINK.UID> == <SRC.MEMORYFINDTYPE.MEMORY_GUILD.LINK.UID>
SYSMESSAGE @0a5a  <ARGS>
ENDIF

[FUNCTION GLIST]
IF (<MEMORYFINDTYPE.MEMORY_GUILD.LINK.UID>)
SRC.SYSMESSAGE @0a5a  Online Guildmembers:
SERV.ALLCLIENTS GLIST_R
ELSE
SRC.SYSMESSAGE @0a5a  Error:
SRC.SYSMESSAGE @07ad  You are not in a guild.
RETURN 1
ENDIF

[FUNCTION GLIST_R] 
IF <MEMORYFINDTYPE.MEMORY_GUILD.LINK.UID> == <SRC.MEMORYFINDTYPE.MEMORY_GUILD.LINK.UID>
SRC.SYSMESSAGE @07ad  <NAME> <MEMORYFINDTYPE.MEMORY_GUILD.LINK.ABBREV>
ENDIF

[EOF]