//-------------------------- /////
////Vendor System - by ClouD /////
//--------------------------
////E-mail: alexbastos@ism.com.br ////
//--------------------------
////ICQ: 133411911 ////
//--------------------------
////Note: Edit only the defs if you're a begginer ////
//--------------------------
////You can edit FUNCTIONs at the end of file
//--------------------------
////Please, do not take this header out :D plz? ////
//-------------------------- /////

///Version 4.6.3///

----------------
----------------
||     /\     ||
||    /  \    ||
||   /    \   ||
||  /      \  ||
|| /  ____  \ ||   Brazil!
||/  |    |  \||
||\  |    |  /||
|| \ |____| / ||
||  \      /  ||
||   \    /   ||
||    \  /    ||
||     \/     ||
----------------
----------------

//Begin of Defs//
///-----------///
//Some things are not in defs, you should try looking for them//

[DEFNAME vendor_sys_switches]
vendoronhouse_onoff 1 ///<-- players can put vendors at their houses (1=YES, 0=No)
vendorshop_onoff 1 ///<-- put 1 if you have a Vendor Shop
vendorsys_money i_gold ///<--- item that will be used as money (default: i_gold)

vendorsys_players_can_access_bank 1 ///(1=yes, 0=no)

vendorsys_maxvendors 1 //how many vendors can players have

[DEFNAME vendor_sys_values]
shardhp www.sphere.com //<-- put your shard site here
shopname Mercado  //<-- You don't have to create this area, just set as it is, or erase it. Players will still be able to create the vendor at them houses.
redeedprice 5000
tax 5 //<-- how much vendor will take of player's money for his works
maxitens 60 // <-- max itens a vendor can carry (you can set to an endless number if want...)
accessmenu_time 360 //<-- time IN SECONDS that players can access the vendor menu

[DEFNAME vendor_sys_msgs] ////Messages
inbagmsg @07a1 The deed must be in your backpack
vendorcheck @07a1 You already have a vendor!
vendorcheck2 @07a1 There's already a vendor near you.
noitem_name None
busy_msg [busy]
noowner This vendor had no owner and has been deleted
already_in_use Vendor is already been used, wait a moment.
closedialog_msg Close the dialog before accesing the vendor
welcome @07a0 Welcome to my shop.
buyfromself @07a0 Do you want to buy from yourself!?
arenotowner You are not my Owner!
hello @07ad Hello Master!
onlyonshop I can stay only at Vendor Shop or your house!
mustputvendor You must put the vendor at your House, or the Vendor Shop.
mustput_athouse You must put the vendor at your House.
adding_item You are already using your vendor.
theitem_cantbeequipped @07ad The item can't be equipped in you.

toofar You are too far from the vendor.
toofar2 You are too far away from the vendor, and the owner wants to use it.
itens_banked1 @07ad Itens have been sent to your bank.
itens_banked2 @07ad The item has been sent to your bank.
noitens_forsale @07ad There're no itens in this vendor.
noitens_forsale_inslot @07a0 There are no itens in this slot
noitens_forsale_inslot10 @07a0 There are no itens in slot <eval <tag.current>+10>
must_buy_separate You must buy the item sepparately

vendor_deleted @07ad Vendor deleted. His money and itens has been sent to your bank.
money_deposit @07a1 Your money has been sent to your bank.

thats_no_item @07a1 That's not an item!
chosse_target_item Choose the item you want to sell.
inbagmsg_item @07a1 The item must be with you.
invalid_item_msg @07ad Invalid item.

invalid_chars @07a1 Invalid letters
invalid_chars_noprice @07a1 You can't put no value to an item
not_enough_money @07ad You don't have enough money
thatsall_tosell That's all he has to sell of the item

cantbuy_noitem @07ad You can't buy 0 itens
cantbuy_minus10 @07a0 You can't buy -10 itens.
thereare_noitens_10 There are no itens in slot <eval <tag.current>+10>!

vendor_closethegumpfirst @07ad Close the menus you have opened first.

[DEFNAME vendor_sys_gump] /// gump text
itensforsale Itens for sale
itenssold Itens sold
profit Profit
withdraw Withdraw
takeitens Take Itens Back
openbank Open Bank
addsitem Add Item
removeedititem Remove/Edit Item
redeedvendor Deed Vendor
caracteristics Vendor Caracteristics
lastsells Last itens sold
vendor_carac_gump Vendor Caracteristics

putprice Put the price for
itemname Name
amountmsg Amnt
pricemsg Price
description_msg Desc.
removemsg Remove
nextmsg Next
backmsg Back
editprice_msg Change Price

lastsales_msg Last Sales
speak_name Speak
colorname_msg Name Hue

put_vendorname_msg Put your vendor name
put_vendorfala_msg Put your vendor speak
color_white_msg White
color_yellow_msg Yellow
color_green_msg Green
color_red_msg Red
color_blue_msg Blue
vendor_color_name Vendor's Name Color

put_description_msg Put description for

owner_name_msg Owner:
see_itensforsale See itens for sale

buy_stock_msg Buy stocked
buy_seeitem_msg See item
buy_seecont_msg See Content
buyall_msg Buy All

soldto sold to

/////////End of DEFS////////

[ITEMDEF i_deed_vendor]
NAME=Deed to a Vendor
ID=i_deed
TYPE=t_normal
VALUE=50000

ON=@Create
color=07ad

on=@dclick
if (<cont>!=<src.findlayer.21>)
src.sysmessage <def.inbagmsg>
return 1
endif
if (<src.tag0.vendedores>==<def.vendorsys_maxvendors>)
src.sysmessage <def.vendorcheck>
return 1
endif
IF (<src.memoryfindtype.memory_gumprecord>)
src.sysmessage <def.vendor_closethegumpfirst>
return 1
ENDIF
IF (<src.flags>&statf_war)
src.flags &= ~statf_war
ENDIF
serv.NEWITEM i_gold
NEW.P=<SRC.P>
IF (<NEW.VENDORCHECK>)
SRC.SYSMESSAGE <def.vendorcheck2>
RETURN 1
endif
IF (<def.vendoronhouse_onoff>)
if (0<uid.<src.region.uid>.more1>==<src>)
serv.newnpc=c_vendor_pod
new.p=<src.p>
FOR 10
try new.tag.last<eval <local._for>>=[none]
ENDFOR
new.tag.housevendor=1
new.tag.owner=<src>
new.tag.fala=I'm <src.name>'s vendor
new.tag.itensavenda=0
new.tag.itensvendidos=0
new.tag.current=0
new.title=<src.name>'s vendor
new.tag.postoem=Created in <rtime.day>/<rtime.month>/<rtime.year>
local.vendor_uid=<new>
src.tag0.vendedores += 1
new.sdialog d_vendor_owner_pod
serv.newitem=i_m_vendorfala
new.cont=<local.vendor_uid>
new.timer=<def.accessmenu_time>
new.tag.player=<src>
src.update
remove
return 1
endif
endif
IF !(<def.vendorshop_onoff>)
src.sysmessage <def.mustput_athouse>
return 1
endif
IF !(<SRC.ISGM>) && (strcmpi(<SRC.REGION.NAME>,<def.shopname>)) && (<def.vendorshop_onoff>)
src.sysmessage <def.mustputvendor>
return 1
endif
if !(strcmpi(<SRC.REGION.NAME>,<def.shopname>)) && (<def.vendorshop_onoff>)
serv.newnpc=c_vendor_pod
new.p=<src.p>
FOR 10
try new.tag.last<eval <local._for>>=[none]
ENDFOR
new.tag.owner=<src>
new.tag.fala=I'm <src.name>'s vendor
new.tag.itensavenda=0
new.tag.itensvendidos=0
new.tag.current=0
new.title=<src.name>'s vendor
new.tag.postoem=Created in <rtime.day>/<rtime.month>/<rtime.year>
local.vendor_uid=<new>
src.tag0.vendedores += 1
new.sdialog d_vendor_owner_pod
serv.newitem=i_m_vendorfala
new.cont=<local.vendor_uid>
new.timer=<def.accessmenu_time>
new.tag.player=<src>
src.update
remove
return 1
endif

/////////Vendor////////////

[CHARDEF c_vendor_pod]
NAME=#NAMES_HUMANMALE
ID=c_man
CAN=mt_nonmover|MT_USEHANDS|MT_EQUIP|MT_FIRE_IMMUNE
ARMOR=200

TSPEECH=vendor_pod

on=@create
COLOR=00
STR=1
INT=0
DEX=0
FAME=100
KARMA=100
ALLSKILLS=0.0
INVUL 1
EQUIPHALO
tag.lucrototal=0
tag.money=0
tag.currentamount=0
MODMAXWEIGHT=5000000

ON=@NPCSEENEWPLAYER
if (<src>==<tag.owner>)
say @0ad Hi master <src.name>
elif !(<src>==<tag.owner>) && (<findid.i_m_falas>)
say @<tag.name.hue> <tag.fala>
endif

on=@npcrestock
ITEM=random_male_hair
color=36
ITEM=i_bagdeitens
ITEM=random_hats
WEIGHT=0
ITEM=random_footwear
WEIGHT=0
ITEM=random_male_pants
WEIGHT=0
ITEM=random_over_armor
WEIGHT=0
ITEM=i_lantern
MOREY=1
TIMER=-1
WEIGHT=0
ITEM=i_m_falas
TIMER=-1

on=@Click
IF (<FLAGS>&statf_war)
flags &= ~statf_war
update
ENDIF
IF (<findid.i_m_vendorfala>) && !(<findid.i_m_vendorfala.timer>==-1)
message <def.busy_msg>
ENDIF
message @<tag.name.hue> <name> (Itens: <eval <tag.itensavenda>>)
return 1

On=@dclick
IF !(<src.isgm>)
return 1
ENDIF

ON=@GetHit
return 1

on=@hittry
return 1

ON=@DEATH
hits=<str>
action -1
return 1

ON=@Attack
return 1

on=@spelleffect
return 1

//////////////////

[SPEECH vendor_pod]
on=buy
LOCAL.P=<P>
P=1,1
P=<LOCAL.P>
IF (<UID.<REGION.UID>.TYPE>==t_multi) && !(<TAG0.HOUSEVENDOR>)
TAG.HOUSEVENDOR=1
ENDIF
IF (<TAG0.HOUSEVENDOR>)
IF (<UID.<REGION.UID>.TYPE>!=t_multi)
IF (<TAG0.ITENSAVENDA>)
serv.newitem=i_backpack
new.name=Vendor Bag
new.color=39
IF (<UID.<TAG0.OWNER>.ISPLAYER>)
new.cont=<uid.<tag0.owner>.findlayer.29>
ELSE
new.p=1,1
new.attr=attr_decay
new.timer 1
ENDIF
f_retirar_itens <new>
ENDIF

IF (<TAG0.MONEY>)
local.tax=<eval (<tag.money>*<def.tax>)/100>
tag.money -= <local.tax>
IF (<UID.<TAG0.OWNER>.ISPLAYER>)
addgold <tag.money> <UID.<TAG0.OWNER>.findlayer.29>
ENDIF
tag.money=0
ENDIF
IF (<UID.<TAG0.OWNER>.ISPLAYER>)
try uid.<tag0.owner>.sysmessage <def.vendor_deleted>
try uid.<tag0.owner>.consumegold <def.redeedprice>
serv.newitem=i_deed_vendor
new.cont=<tag0.owner>
try uid.<tag0.owner>.tag.vendedores
ENDIF
REMOVE
RETURN 1
ENDIF
ENDIF

if (<distance> > 2)
return 1
endif
IF !(<uid.<tag.owner>.isplayer>)
src.sysmessage <def.noowner>
remove
return 1
endif
IF (<tag0.itensavenda>)
FOR <tag0.itensavenda>
IF (<UID.<TAG0.IUID<EVAL <LOCAL._FOR>>>.DISPID>!=<UID.<TAG0.IUID<EVAL <LOCAL._FOR>>>.TAG0.DISPID2>)
TRY UID.<TAG0.IUID<EVAL <LOCAL._FOR>>>.DISPID=<UID.<TAG0.IUID<EVAL <LOCAL._FOR>>>.TAG0.DISPID2>
TRY UID.<TAG0.IUID<EVAL <LOCAL._FOR>>>.UPDATE
ENDIF
ENDFOR
ENDIF
IF (<f_players_perto>)
src.sysmessage <def.already_in_use>
return 1
endif
if (<src.memoryfindtype.memory_gumprecord> )
src.sysmessage <def.closedialog_msg>
return 1
endif
if (<src>!=<tag.owner>)
src.sysmessage <def.welcome>
tag.currentamount=0
tag.itemlist2=0
sdialog d_vendor_podprinc
serv.newitem=i_m_vendorfala
new.tag.player=<src>
new.timer=<def.accessmenu_time>
new.cont=<uid>
else
say <def.buyfromself>
endif

on=stock
LOCAL.P=<P>
P=1,1
P=<LOCAL.P>
IF (<UID.<REGION.UID>.TYPE>==t_multi) && !(<TAG0.HOUSEVENDOR>)
TAG.HOUSEVENDOR=1
ENDIF
IF (<TAG0.HOUSEVENDOR>)
IF (<UID.<REGION.UID>.TYPE>!=t_multi)
IF (<TAG0.ITENSAVENDA>)
serv.newitem=i_backpack
new.name=Vendor Bag
new.color=39
IF (<UID.<TAG0.OWNER>.ISPLAYER>)
new.cont=<uid.<tag0.owner>.findlayer.29>
ELSE
new.p=1,1
new.attr=attr_decay
new.timer 1
ENDIF
f_retirar_itens <new>
ENDIF
IF (<TAG0.MONEY>)
local.tax=<eval (<tag.money>*<def.tax>)/100>
tag.money -= <local.tax>
IF (<UID.<TAG0.OWNER>.ISPLAYER>)
addgold <tag.money> <UID.<TAG0.OWNER>.findlayer.29>
ENDIF
tag.money=0
ENDIF
IF (<UID.<TAG0.OWNER>.ISPLAYER>)
try uid.<tag0.owner>.sysmessage <def.vendor_deleted>
try uid.<tag0.owner>.consumegold <def.redeedprice>
serv.newitem=i_deed_vendor
new.cont=<tag0.owner>
try uid.<tag0.owner>.tag.vendedores=
ENDIF
REMOVE
RETURN 1
ENDIF
ENDIF
IF (<tag0.itensavenda>)
FOR <tag0.itensavenda>
IF (<UID.<TAG0.IUID<EVAL <LOCAL._FOR>>>.DISPID>!=<UID.<TAG0.IUID<EVAL <LOCAL._FOR>>>.TAG0.DISPID2>)
TRY UID.<TAG0.IUID<EVAL <LOCAL._FOR>>>.DISPID=<UID.<TAG0.IUID<EVAL <LOCAL._FOR>>>.TAG0.DISPID2>
TRY UID.<TAG0.IUID<EVAL <LOCAL._FOR>>>.UPDATE
ENDIF
ENDFOR
ENDIF
If (<src.isgm>) && !(<findid.i_m_vendorfala>)
serv.newitem=i_m_vendorfala
new.tag.player=<src>
new.timer=360
new.cont=<uid>
tag.current=1
sdialog d_vendor_owner_pod
return 1
endif
if (<distance> > 2)
return 1
endif
if (<src.memoryfindtype.memory_gumprecord> )
src.sysmessage <def.closedialog_msg>
return 1
endif
if (<src>!=<tag.owner>)
say <def.arenotowner>
return 1
endif
If (<src.findid.i_memory_ivendor>)
src.sysmessage <def.adding_item>
return 1
endif
if (<src>==<tag.owner>)
if (<findid.i_m_vendorfala>) && !(<uid.<findid.i_m_vendorfala.tag.player>.isonline>)
findid.i_m_vendorfala.remove
elif (<findid.i_m_vendorfala>) && (<uid.<findid.i_m_vendorfala.tag.player>.isonline>) && (<findid.i_m_vendorfala.tag.player>!=<SRC>)
try uid.<findid.i_m_vendorfala.tag.player>.sysmessage @07a1 <src.name> wants to use <SRC.SEX his/her> vendor, excuse me.
findid.i_m_vendorfala.timer=0
serv.newitem=i_m_vendorfala
new.tag.player=<src>
new.timer=<def.accessmenu_time>
new.cont=<uid>
endif
tag.current=1
tag.itemlist2=0
sdialog d_vendor_owner_pod
serv.newitem=i_m_vendorfala
new.tag.player=<src>
new.timer=<def.accessmenu_time>
new.cont=<uid>
say <def.hello>
endif

on=come
if (<src>!=<tag.owner>)
say <def.arenotowner>
return 1
ENDIF
SERV.NEWITEM i_gold
NEW.P=<SRC.P>
NEW.LINK=<uid>
IF (<NEW.VENDORCHECK>)
SRC.SYSMESSAGE <def.vendorcheck2>
RETURN 1
ENDIF
if (<uid.<src.region.uid>.more1>==<src>) && (<src>==<tag.owner>)
p=<src.p>
update
src.update
return 1
endif
If !(<def.vendorshop_onoff>)
return 1
endif
if (<src>==<tag.owner>) 
IF (strcmpi(<SRC.REGION.NAME>,<def.shopname>))
say <def.onlyonshop>
else
p=<src.p>
src.update
update
endif
endif

on=help
src.weblink <def.shardhp>

///////////////////////

[DIALOG d_vendor_owner_pod]
140, 0
PAGE 0
resizepic 55 87 3500 300 335
dtext 88 132 46 <def.takeitens>
button 70 135 2117 2118 1 0 1
IF (<def.vendorsys_players_can_access_bank>)
button 70 165 2117 2118 1 0 2
dtext 88 163 46 <def.openbank>
ENDIF
button 70 195 2117 2118 1 0 3
dtext 88 191 46 <def.addsitem>
button 70 225 2117 2118 1 0 4
dtext 88 220 46 <def.removeedititem>
button 70 255 2117 2118 1 0 5
dtext 88 252 46 <def.redeedvendor> (<eval <def.redeedprice>/1000>k)
button 229 370 4005 4006 1 0 6
dtext 160 390 270 <def.caracteristics>
dtext 107 99 1345 Vendor System
dtext 88 300 59 <def.itensforsale>: <eval <tag.itensavenda>>
dtext 88 320 59 <def.itenssold>: <eval <tag.itensvendidos>>
dtext 88 340 59 <def.profit>: <eval <tag.lucrototal>> <serv.itemdef.<def.vendorsys_money>.name>s
button 70 283 2117 2118 1 0 7
dtext 88 280 46 <def.withdraw> (<eval <tag.money>> <serv.itemdef.<def.vendorsys_money>.name>s)
dhtmlgump 227 100 190 50 0 0 - <strsub 1 2 bb><strsub 1 2 yy> <strsub 1 2 0C><strsub 1 2 ll>ou<strsub 1 2 0D>

[DIALOG d_vendor_owner_pod BUTTON]
on=0
findid.i_m_vendorfala.remove

on=1
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
If !(<tag0.itensavenda>)
src.sysmessage <def.noitens_forsale>
sdialog d_vendor_owner_pod
return 1
endif
local.lastp=<src.p>
local.flags=<src.region.flags>
src.region.flags=0
src.p 1,1
serv.newitem=i_backpack
new.name=Vendor Bag
new.color=39
new.cont=<src.findlayer.29>
f_retirar_itens <new>
tag.itensavenda=0
src.sysmessage <def.itens_banked1>
findid.i_m_vendorfala.remove
src.p=<local.lastp>
src.region.flags=<local.flags>
src.update

on=2
IF (<def.vendorsys_players_can_access_bank>)
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
src.bankself
sdialog d_vendor_owner_pod
endif
endif

on=3
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
serv.newitem=i_memory_ivendor
new.link=<uid>
new.equip
new.timer=60
endif

on=4
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
If !(<tag0.itensavenda>)
src.sysmessage <def.noitens_forsale>
sdialog d_vendor_owner_pod
else
sdialog d_remover_item1
endif

on=5
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
if (<src.totalgold> < <def.redeedprice>)
src.sysmessage <def.not_enough_money>
sdialog d_vendor_owner_pod
return 1
endif
IF (<tag.money>)
local.tax=<eval (<tag.money>*<def.tax>)/100>
tag.money -= <local.tax>
addgold <tag.money> <src.findlayer.29>
tag.money=0
ENDIF
local.lastp=<src.p>
local.flags=<src.region.flags>
src.region.flags=0
src.p 1,1
src.consumegold <def.redeedprice>
IF (<tag0.itensavenda>)
serv.newitem=i_backpack
new.name=Vendor Bag
new.color=39
new.cont=<src.findlayer.29>
f_retirar_itens <new>
ENDIF
src.sysmessage <def.vendor_deleted>
serv.newitem=i_deed_vendor
new.bounce
src.tag.vendedores -= 1
IF !(<src.tag0.vendedores>)
src.tag.vendedores=
ENDIF
remove
src.p=<local.lastp>
src.region.flags=<local.flags>
src.update

on=6
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
sdialog d_caracteristicas_vendor 1
endif

on=7
IF !(<tag.money>)
sdialog d_vendor_owner_pod
else
local.tax=<eval (<tag.money>*<def.tax>)/100>
tag.money -= <local.tax>
addgold <tag.money> <src.findlayer.29>
tag.money=0
src.sysmessage The Vendor took <eval <def.tax>>% (<eval <local.tax>>) of the profits for his work.
src.sysmessage <def.money_deposit>
sdialog d_vendor_owner_pod
src.update
endif

//////////////

[itemdef i_memory_ivendor]
NAME=Item Selector
ID=i_handr_1
TYPE=t_eq_script
layer=layer_special

on=@create
attr=attr_invis|attr_decay

on=@equip
target @07a3 <def.chosse_target_item>

on=@targon_cancel
link.findid.i_m_vendorfala.remove
remove

ON=@targon_char
src.sysmessage <def.thats_no_item>
link.findid.i_m_vendorfala.remove
remove
return 1

on=@targon_item
If (<link.distance> > 4)
src.sysmessage <def.toofar>
link.findid.i_m_vendorfala.remove
remove
return 1
endif
IF (<src.targ.cont>==<src>)
src.sysmessage <def.theitem_cantbeequipped>
link.findid.i_m_vendorfala.remove
remove
return 1
endif
if (<src.targ.topobj.uid>!=<src>)
src.sysmessage <def.inbagmsg_item>
link.findid.i_m_vendorfala.remove
remove
return 1
endif
if (<src.targ.baseid>==<def.vendorsys_money>)
src.sysmessage <def.invalid_item_msg>
link.findid.i_m_vendorfala.remove
remove
return 1
endif
if (<link.tag.itensavenda> > <def.maxitens>)
src.sysmessage @07a0 Vendor already has <eval <def.maxitens>> itens with him.
link.findid.i_m_vendorfala.remove
remove
return 1
endif
if (<src.targ.topobj.uid>==<src>) && !(<eval <link.tag0.iuid<eval <link.tag0.itensavenda>+1>>>)
local.typevend=<src.targ.type>
local.corvend=<src.targ.color>
local.morevend=<src.targ.more1>
src.targ.more1=<eval {1 999999}>
src.targ.color=-1
src.targ.type=t_script
src.targ.cont=<link.findlayer.21>
try link.tag.iuid<eval <link.tag.itensavenda>+1>=<src.targ>
try link.tag.preco<eval <link.tag.itensavenda>+1>=<src.targ.value>
link.tag0.itensavenda += 1
src.targ.type=<local.typevend>
src.targ.color=<local.corvend>
src.targ.more1=<local.morevend>
src.targ.tag.dispid2=<src.targ.dispid>
link.tag.current=<link.tag.itensavenda>
link.sdialog d_vendor_price1
remove
return 1
endif

ON=@targon_ground
src.sysmessage <def.thats_no_item>
link.findid.i_m_vendorfala.remove
remove
return 1

on=@timer
remove
return 1

//////////////////////

[DIALOG d_vendor_price1]
10,30
noclose
page 0
resizepic 185 190 3600 340 140
resizepic 195 200 3500 320 120
dtext 230 210 0 Coloque o preco para <uid.<tag0.iuid<eval <tag.current>>>.name>
dtextentry 270 250 300 25 55 0 <eval <tag.preco<eval <tag0.current>>>>
button 250 250 5224 5003 1 0 1

[DIALOG d_vendor_price1 BUTTON]
on=0
findid.i_m_vendorfala.remove

on=1
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
if (0<ISEMPTY <ARGTXT[0]>>)
src.sysmessage <def.invalid_chars>
sdialog d_vendor_price1
return 1
endif
IF (0<ISBADSTR <ARGTXT[0]>>) || (STRMATCH(*[a-z]*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
sdialog d_vendor_price1
return 1
endif
if (STRMATCH(0*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars_noprice>
sdialog d_vendor_price1
return 1
endif
if !(strcmpi(<ARGTXT[0]>,0))
src.sysmessage <def.invalid_chars_noprice>
sdialog d_vendor_price1
return 1
endif
try tag.preco<eval <tag.current>>=<strsub 0 9 <argtxt[0]>>
sdialog d_remover_item1

//////////////Remover itens//////////////////

[DIALOG d_remover_item1]
210, 100
PAGE 1
resizepic 15 70 3500 390 235
tilepic 75 80 <uid.<tag.iuid<eval <tag.current>>>.id>
dtext 101 128 1152 <uid.<tag.iuid<eval <tag.current>>>.name>
dtext 49 128 102 <def.itemname>:
dtext 100 155 1152 <uid.<tag.iuid<eval <tag.current>>>.amount>
dtext 49 154 102 <def.amountmsg>:
dtext 49 181 102 <def.pricemsg>:
dtext 99 182 1152 <eval <tag.preco<eval <tag.current>>>>
dtext 49 210 102 <def.description_msg>:
dcroppedtext 90 210 110 110 1152 <QVAL <ISEMPTY <tag.desc<eval <tag.current>>>> ? ...:<tag.desc<eval <tag.current>>>>
button 63 250 249 248 1 0 1
dtext 67 272 32 <def.removemsg>
button 341 251 4005 4006 1 0 2
dtext 337 272 32 <def.nextmsg>
button 242 251 4014 4015 1 0 3
dtext 240 272 32 <def.backmsg>
button 242 161 4005 4006 1 0 4
dtext 276 161 88 <def.editprice_msg>
dtext 276 90 55 Iten <eval <tag.current>>
button 30 213 01519 01519 1 0 5
IF (<tag.itensavenda> > 9)
button 341 221 4005 4006 1 0 6
button 242 221 4014 4015 1 0 7
ENDIF

[DIALOG d_remover_item1 BUTTON]
on=0
findid.i_m_vendorfala.remove

on=1
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
if !(0<tag.iuid<eval <tag.current>>>)
src.sysmessage <def.noitens_forsale_inslot>
findid.i_m_vendorfala.remove
return 1
endif
local.lastp=<src.p>
local.flags=<src.region.flags>
src.region.flags=0
src.p=1,1
try tag.preco<eval <tag.current>>
try uid.<tag.iuid<eval <tag.current>>>.cont=<src.findlayer.29>
try uid.<tag.iuid<eval <tag.current>>>.tag.dispid2
try tag.iuid<eval <tag.current>>
try tag.desc<eval <tag.current>>
tag.current -= 1
src.sysmessage <def.itens_banked1>
f_ajeitar_itens
tag.itensavenda -= 1
findid.i_m_vendorfala.remove
src.bankself
src.p=<local.lastp>
src.region.flags=<local.flags>
src.update

on=2
if !(0<tag.iuid<eval <tag.current>+1>>)
tag.current=1
sdialog d_remover_item1
return 1
endif
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
tag.current += 1
sdialog d_remover_item1

on=3
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
if (<tag.current> > 1)
tag.current -= 1
sdialog d_remover_item1
elif (<tag.current>==1)
tag.current=<tag.itensavenda>
sdialog d_remover_item1
endif

on=4
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
if !(0<tag.iuid<eval <tag.current>>>)
src.sysmessage <def.noitens_forsale_inslot>!
sdialog d_vendor_owner_pod
else
sdialog d_vendor_price1
endif

on=5
if !(0<tag.iuid<eval <tag.current>>>)
src.sysmessage <def.noitens_forsale_inslot>!
sdialog d_vendor_owner_pod
else
tag.algarismo=<eval <tag.current>>
sdialog d_mudardesc1
endif

on=6
if !(0<tag.iuid<eval <tag.current>+10>>)
tag.current=<tag.itensavenda>
sdialog d_remover_item1
return 1
endif
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
tag.current += 10
sdialog d_remover_item1

on=7
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
if !(0<tag.iuid<eval <tag.current>-10>>)
tag.current=1
sdialog d_remover_item1
return 1
endif
if (<tag.current> > 10)
tag.current -= 10
sdialog d_remover_item1
return 1
endif
if (<tag.current>==1)
tag.current=<tag.itensavenda>
sdialog d_remover_item1
endif

//////////////////Caracteristicas Vendor///////////////

[DIALOG d_caracteristicas_vendor]
170, 55
PAGE 0
resizepic 39 84 2600 330 270

PAGE 1
dtext 114 98 55 <def.vendor_carac_gump>
dtext 75 130 1152 <def.itemname>: <name>
dtext 75 159 1152 <tag.postoem>
dtext 75 187 1152 <def.speak_name> <QVAL <FINDID.i_m_falas.UID> ? On : Off>
dtext 75 231 1152 <def.colorname_msg>:
dtext 168 231 <eval <tag.name.hue>> COLOR
dtext 75 207 1152 <def.speak_name>:
dtext 110 207 1152 <tag.fala>
button 58 133 2117 2118 1 0 1
button 58 209 2117 2118 1 0 2
button 58 233 2117 2118 1 0 3
button 55 275 4005 4006 1 0 4
dtext 85 276 66 <def.lastsales_msg>
button 58 187 2117 2118 1 0 5
button 90 308 4016 4015 1 0 6
dtext 120 310 1152 <def.backmsg>
button 58 253 2117 2118 1 0 7
dtext 78 250 1152 Dress the vendor like you

[DIALOG d_caracteristicas_vendor BUTTON]
on=0
findid.i_m_vendorfala.remove

on=1
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
sdialog d_mudarnome_vendor
endif

on=2
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
sdialog d_mudarfala_vendor
endif

on=3
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
sdialog d_mudarcor_vendor
endif

on=4
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
sdialog d_lastvendor
endif

on=5
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
If (<findid.i_m_falas>)
findid.i_m_falas.remove
sdialog d_caracteristicas_vendor 1
return 1
endif
If !(<findid.i_m_falas>)
serv.newitem=i_m_falas
new.cont=<uid>
sdialog d_caracteristicas_vendor 1
endif

on=6
sdialog d_vendor_owner_pod

on=7
FOR 24
IF (<local._for>!=9) && (<local._for>!=11) && (<local._for>!=15) && (<local._for>!=18) && (<local._for>!=21)
IF (<SRC.FINDLAYER.<EVAL <LOCAL._FOR>>.DISPID>!=i_deathshroud)
TRY FINDLAYER.<EVAL <LOCAL._FOR>>.REMOVE
IF (<SRC.FINDLAYER.<EVAL <LOCAL._FOR>>>)
SERV.NEWITEM=<SRC.FINDLAYER.<EVAL <LOCAL._FOR>>.DISPID>
NEW.COLOR=<SRC.FINDLAYER.<EVAL <LOCAL._FOR>>.COLOR>
NEW.NAME=<SRC.FINDLAYER.<EVAL <LOCAL._FOR>>.NAME>
NEW.CONT=<UID>
ENDIF
ENDIF
ENDIF
ENDFOR
color=<src.color>
oskin=<src.oskin>
sdialog d_caracteristicas_vendor 1

//////////////////////////////

[DIALOG d_mudarnome_vendor]
10,30
page 0
resizepic 195 200 2620 320 120
dtext 250 210 77 <def.put_vendorname_msg>
dtextentry 270 250 300 25 99 0 <name>
button 250 250 5224 5003 1 0 1

[DIALOG d_mudarnome_vendor BUTTON]
on=0
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
sdialog d_caracteristicas_vendor
endif

on=1
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
if (0<ISEMPTY <ARGTXT[0]>>)
src.sysmessage @07ad The name can't be empty
sdialog d_mudarnome_vendor
elif (0<ISBADSTR <ARGTXT[0]>>) || (STRMATCH(*[0-9]*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
sdialog d_mudarnome_vendor
elif (STRMATCH(Seer*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(Conselour*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(Admin*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(Staff*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(GM*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(@*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(EOF*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif !(strcmpi(<ARGTXT[0]>,EOF))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
else
name=<strsub 0 15 <argtxt[0]>>
sdialog d_caracteristicas_vendor
endif

//////////////////////

[DIALOG d_mudarfala_vendor]
10,30
page 0
resizepic 195 200 2620 320 120
dtext 250 210 77 <def.put_vendorfala_msg>
dtextentry 270 250 300 25 99 0 <tag0.fala>
button 250 250 5224 5003 1 0 1

[DIALOG d_mudarfala_vendor BUTTON]
on=0
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
sdialog d_caracteristicas_vendor
endif

on=1
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
if (0<ISEMPTY <ARGTXT[0]>>)
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (0<ISBADSTR <ARGTXT[0]>>) || (STRMATCH(*[0-9]*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
sdialog d_mudarfala_vendor
elif (STRMATCH(*Seer*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(*Conselour*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(*Admin*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(Staff*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(*GM*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(@*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif (STRMATCH(EOF*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
elif !(strcmpi(<argtxt[0]>,EOF))
src.sysmessage <def.invalid_chars>
findid.i_m_vendorfala.remove
else
tag.fala=<strsub 0 30 <argtxt[0]>>
sdialog d_caracteristicas_vendor
endif

///////////////////////////

[DIALOG d_mudarcor_vendor]
0, 0
PAGE 0
gumppic 110 122 7
button 205 153 210 211 1 0 1
dtext 229 153 1152 <def.color_white_msg>
button 206 183 210 211 1 0 2
dtext 229 182 152 <def.color_yellow_msg>
button 207 214 210 211 1 0 3
dtext 231 214 162 <def.color_green_msg>
button 208 246 210 211 1 0 4
dtext 233 246 32 <def.color_red_msg>
button 209 275 210 211 1 0 5
dtext 235 275 102 <def.color_blue_msg>
dtext 174 124 1350 <def.vendor_color_name>

[DIALOG d_mudarcor_vendor BUTTON]
on=0
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
sdialog d_caracteristicas_vendor
endif

on=1
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
tag.name.hue=07a1
sdialog d_caracteristicas_vendor
endif

on=2
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
tag.name.hue=55
sdialog d_caracteristicas_vendor
endif

on=3
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
tag.name.hue=0ad
sdialog d_caracteristicas_vendor
endif

on=4
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
tag.name.hue=07a3
sdialog d_caracteristicas_vendor
endif

on=5
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
tag.name.hue=88
sdialog d_caracteristicas_vendor
endif

////////////Descrição///////////

[DIALOG d_mudardesc1]
10,30
page 0
resizepic 195 200 2620 320 120
dtext 250 210 77 <def.put_description_msg> <uid.<tag.iuid<eval <tag.algarismo>>>.name>
dtextentry 220 250 640 85 99 0 <QVAL <ISEMPTY <tag.desc<eval <tag0.algarismo>>>> ? ...:<tag.desc<eval <tag0.algarismo>>>>
button 200 250 5224 5003 1 0 1

[DIALOG d_mudardesc1 BUTTON]
on=0
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
else
sdialog d_vendor_owner_pod
endif

on=1
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
if (0<ISEMPTY <ARGTXT[0]>>)
src.sysmessage <def.invalid_chars>
sdialog d_mudardesc1
elif (0<ISBADSTR <ARGTXT[0]>>) || (STRMATCH(*[0-9]*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
sdialog d_mudardesc1
elif (STRMATCH(EOF*,<ARGTXT[0]>))
src.sysmessage <def.invalid_chars>
sdialog d_mudardesc1
elif !(strcmpi(<ARGTXT[0]>,EOF))
src.sysmessage <def.invalid_chars>
sdialog d_mudardesc1
else
try tag.desc<eval <tag.algarismo>>=<ARGTXT[0]> .
sdialog d_remover_item1
tag.algarismo=
endif

///////////////Comprador///////////////

[DIALOG d_vendor_podprinc]
210, 100
PAGE 1
resizepic 20 30 3500 395 270
tilepic 260 90 3823
tilepic 280 90 3823
tilepic 270 105 3823
tilepic 250 105 3826
tilepic 290 105 3826
dtext 49 80 0ad <def.itemname>:
dtext 93 80 1152 <name>
dtext 49 110 0ad <def.owner_name_msg>
dtext 97 110 1152 <uid.<tag.owner>.name>
dtext 49 140 0ad <tag.postoem>
dtext 49 170 0ad <def.itensforsale>:
dtext 153 170 1152 <eval <tag.itensavenda>>
dtext 49 200 0ad <def.itenssold>:
dtext 123 200 1152 <eval <tag.itensvendidos>>
IF (<tag.itensavenda>)
button 222 261 4005 4006 1 0 1
dtext 252 260 39 <def.see_itensforsale>
ENDIF
dtext 49 230 0ad <def.profit>:
dtext 97 230 1152 <eval <tag.lucrototal>>
dtext 120 40 036 V<strsub 1 2 ee>ndor S<strsub 1 2 yy>stem - <strsub 1 2 bb><strsub 1 2 yy> <strsub 1 2 0C><strsub 1 2 ll>ou<strsub 1 2 0D>
dtext 75 255 2010 Last Sales
button 45 253 4005 4006 0 2 2
IF (<tag.itensavenda>)
button 222 231 4005 4006 0 3 4
dtext 252 230 39 Quick List
ENDIF

PAGE 2
resizepic 20 30 3500 395 270
CHECKERTRANS 20 30 395 270
dtext 180 40 036 Last Sales
FOR 10
dtext 50 <eval (<local._for>*20)+40> 1152 <tag0.last<eval <local._for>>>
ENDFOR
button 360 265 4016 4015 0 1 3

PAGE 3
resizepic 20 30 3500 395 270
CHECKERTRANS 20 30 395 270
dtext 180 40 036 Quick List
FOR 10
local.blah=<EVAL <TAG0.ITEMLIST2>+<LOCAL._FOR>>
IF (<TAG0.IUID<EVAL <LOCAL.BLAH>>>!=0)
dtext 70 <eval (<local._for>*20)+40> 1152 <UID.<TAG0.IUID<EVAL <LOCAL.BLAH>>>.AMOUNT> <UID.<TAG0.IUID<EVAL <LOCAL.BLAH>>>.NAME> (N: <EVAL <LOCAL.BLAH>>)
button 40 <eval (<local._for>*20)+39> 4005 4006 1 0 <eval 6+<local._for>>
ENDIF
ENDFOR
IF !(<EVAL <TAG0.ITEMLIST2>+10> > 10)
button 40 265 4016 4015 0 1 3
ELSE
button 40 265 4016 4015 1 0 6
ENDIF
IF (<TAG0.ITENSAVENDA> > <EVAL <TAG0.ITEMLIST2>+10>)
button 360 265 4005 4006 1 0 5
ENDIF

[DIALOG d_vendor_podprinc BUTTON]
on=0
TAG.ITEMLIST2=0
findid.i_m_vendorfala.remove

ON=1
tag.current=1
sdialog d_vendor_pod1

on=5
TAG.ITEMLIST2 += 10
SDIALOG d_vendor_podprinc 3

ON=6
TAG.ITEMLIST2 -= 10
IF (<TAG.ITEMLIST2> < 1)
TAG.ITEMLIST2=0
SDIALOG d_vendor_podprinc
ELSE
SDIALOG d_vendor_podprinc 3
ENDIF

ON=7 16
tag.current=<eval (<argn1>-6)+<tag.itemlist2>>
TAG.ITEMLIST2=0
SDIALOG d_vendor_pod1

//////////////////////////////

[DIALOG d_vendor_pod1]
210, 100
nomove
PAGE 0
gumppic 15 70 04cc
gumppic 140 0 064
tilepic 180 30 <uid.<tag0.iuid<eval <tag0.current>>>.id>
dtext 101 129 1152 <uid.<tag.iuid<eval <tag.current>>>.name>
dtext 51 128 102 <def.itemname>:
dtext 100 155 1152 <uid.<tag.iuid<eval <tag.current>>>.amount>
dtext 49 154 102 <def.amountmsg>:
dtext 49 181 102 <def.pricemsg>:
dtext 99 182 1152 <eval <tag.preco<eval <tag.current>>>> <serv.itemdef.<def.vendorsys_money>.name>
dtext 49 210 102 <def.description_msg>:
dcroppedtext 90 210 110 110 1152 <QVAL <ISEMPTY <tag.desc<eval <tag.current>>>> ? ...:<tag.desc<eval <tag.current>>>>
IF (<uid.<tag.iuid<eval <tag.current>>>.amount>==1)
button 63 250 249 248 1 0 1
dtext 67 272 32 <def.buyall_msg>
ENDIF
button 341 281 4005 4006 1 0 2
dtext 340 302 32 <def.nextmsg>
IF (<uid.<tag.iuid<eval <tag.current>>>.amount> > 1)
button 291 125 00d8 00d8 1 0 3
dtext 293 122 88 +
dtext 307 121 55 <eval <tag.currentamount>>
button 271 125 00d8 00d8 1 0 4
dtext 273 122 88 -
button 263 170 249 248 1 0 5
dtext 253 190 59 <def.buy_stock_msg>
button 291 145 00d8 00d8 1 0 6
dtext 293 142 32 +
button 271 145 00d8 00d8 1 0 7
dtext 273 142 32 -
ENDIF
IF !(<uid.<tag.iuid<eval <tag.current>>>.type>==t_container)
button 271 220 00d8 00d8 1 0 8
dtext 285 216 39 <def.buy_seeitem_msg>
ENDIF
button 35 214 00d8 00d8 1 0 9
button 242 281 4014 4015 1 0 10
dtext 240 302 32 <def.backmsg>
IF (<uid.<tag.iuid<eval <tag.current>>>.amount> > 1)
gumppic 52 263 033
dtext 57 272 1152 <eval <tag.preco<eval <tag.current>>>*<tag.currentamount>>
endif
IF (<tag.itensavenda> > 9)
button 341 251 4005 4006 1 0 11
button 242 251 4014 4015 1 0 12
ENDIF
dtext 50 100 033 Item <eval <tag.current>>
IF (<uid.<tag.iuid<eval <tag.current>>>.type>==t_container)
button 271 220 00d8 00d8 1 0 13
dtext 285 216 39 <def.buy_seecont_msg>
ENDIF

[DIALOG d_vendor_pod1 BUTTON]
On=0
src.dialogclose d_descricao1
findid.i_m_vendorfala.remove

on=1
If (<distance> > 5)
src.sysmessage <def.toofar>
src.dialogclose d_descricao1
findid.i_m_vendorfala.remove
return 1
endif
if !(0<tag.preco<eval <tag.current>>>)
src.sysmessage <def.noitens_forsale_inslot>
src.dialogclose d_descricao1
findid.i_m_vendorfala.remove
return 1
endif
if (<uid.<tag.iuid<eval <tag.current>>>.amount> > 1)
src.sysmessage <def.must_buy_separate>
src.dialogclose d_descricao1
findid.i_m_vendorfala.remove
return 1
endif
IF (<src.totalgold> < <eval <tag.preco<eval <tag.current>>>>)
say <def.not_enough_money>
src.dialogclose d_descricao1
findid.i_m_vendorfala.remove
else
OBJ=<tag.iuid<eval <tag.current>>>
LOCAL.LASTP=<SRC.P>
LOCAL.FLAGS=<SRC.REGION.FLAGS>
src.region.flags=0
SRC.P=1,1
FOR 10 1
IF (<LOCAL._FOR>!=1)
TRY TAG.LAST<EVAL <LOCAL._FOR>>=<TAG.LAST<EVAL <LOCAL._FOR>-1>>
ELSE
tag.last1=<obj.name> <def.soldto> <src.name>
ENDIF
ENDFOR
tag.lucrototal += <tag.preco<eval <tag.current>>>
LOCAL.PRECO=<tag.preco<eval <tag.current>>>
tag.money += <tag.preco<eval <tag.current>>>
say @07ad <obj.name> vendido para <src.name>!
try tag.preco<eval <tag.current>>
try tag.desc<eval <tag.current>>
obj.cont=<src.findlayer.29>
obj.tag.dispid2
try tag.iuid<eval <tag.current>>
tag.itensvendidos += 1
src.sysmessage <def.itens_banked2>
tag.current -= 1
src.dialogclose d_descricao1
f_ajeitar_itens
tag.itensavenda -= 1
findid.i_m_vendorfala.remove
src.CONSUMEGOLD <LOCAL.PRECO>
src.bankself
src.p=<local.lastp>
src.region.flags=<local.flags>
src.update
endif

on=2
src.dialogclose d_descricao1
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
IF !(<tag0.iuid<eval <tag.current>+1>>)
tag.current=1
tag.currentamount=0
sdialog d_vendor_pod1
else
tag.currentamount=0
tag.current += 1
sdialog d_vendor_pod1
endif

on=3
src.dialogclose d_descricao1
if (<tag.currentamount>==<uid.<tag.iuid<eval <tag.current>>>.amount>)
src.sysmessage <def.thatsall_tosell>
sdialog d_vendor_pod1
return 1
endif
if (<tag.currentamount> > <uid.<tag.iuid<eval <tag.current>>>.amount>)
tag.currentamount=<uid.<tag.iuid<eval <tag.current>>>.amount>
sdialog d_vendor_pod1
else
tag.currentamount += 1
sdialog d_vendor_pod1
endif

on=4
if !(<tag0.currentamount>)
tag.currentamount=0
sdialog d_vendor_pod1
else
tag.currentamount -= 1
sdialog d_vendor_pod1
endif
src.dialogclose d_descricao1

on=5
If (<distance> > 5)
src.sysmessage <def.toofar>
src.dialogclose d_descricao1
findid.i_m_vendorfala.remove
return 1
endif
if !(<tag0.preco<eval <tag.current>>>)
src.sysmessage <def.noitens_forsale_inslot>
src.dialogclose d_descricao1
findid.i_m_vendorfala.remove
return 1
endif
if !(<tag0.currentamount>)
src.sysmessage <def.cantbuy_noitem>
src.dialogclose d_descricao1
findid.i_m_vendorfala.remove
return 1
endif
IF (<src.totalgold> < <eval <tag.preco<eval <tag.current>>>*<tag.currentamount>>)
say <def.not_enough_money>
src.dialogclose d_descricao1
findid.i_m_vendorfala.remove
return 1
endif
OBJ=<tag.iuid<eval <tag.current>>>
LOCAL.LASTP=<SRC.P>
LOCAL.FLAGS=<SRC.REGION.FLAGS>
src.region.flags=0
SRC.P=1,1
IF (<tag.currentamount>==<obj.amount>)
FOR 10 1
IF (<LOCAL._FOR>!=1)
TRY TAG.LAST<EVAL <LOCAL._FOR>>=<TAG.LAST<EVAL <LOCAL._FOR>-1>>
ELSE
tag.last1=<eval <tag.currentamount>> <obj.name> <def.soldto> <src.name>
ENDIF
ENDFOR
tag.lucrototal += <tag.preco<eval <tag.current>>>
LOCAL.PRECO=<eval <tag.preco<eval <tag.current>>>*<tag.currentamount>>
tag.money += <eval <tag.preco<eval <tag.current>>>*<tag.currentamount>>
say @07ad <obj.name> vendido para <src.name>!
try tag.desc<eval <tag.current>>
try tag.preco<eval <tag.current>>
tag0.itensvendidos += 1
obj.cont=<src.findlayer.29>
obj.tag.dispid2
try tag.iuid<eval <tag.current>>
tag.current -= 1
f_ajeitar_itens
tag.itensavenda -= 1
src.sysmessage <def.itens_banked2>
src.dialogclose d_descricao1
findid.i_m_vendorfala.remove
src.CONSUMEGOLD <LOCAL.PRECO>
src.bankself
src.p=<local.lastp>
src.region.flags=<local.flags>
src.update
else
FOR 10 1
IF (<LOCAL._FOR>!=1)
TRY TAG.LAST<EVAL <LOCAL._FOR>>=<TAG.LAST<EVAL <LOCAL._FOR>-1>>
ELSE
tag.last1=<eval <tag.currentamount>> <obj.name> <def.soldto> <src.name>
ENDIF
ENDFOR
tag.lucrototal += <eval <tag.preco<eval <tag.current>>>*<tag.currentamount>>
LOCAL.PRECO=<eval <tag.preco<eval <tag.current>>>*<tag.currentamount>>
tag0.money += <local.preco>
serv.newdupe=<obj>
new.amount=<tag.currentamount>
new.cont=<src.findlayer.29>
say @07ad <eval <tag.currentamount>> <obj.name> vendidos para <src.name>!
obj.amount -= <tag.currentamount>
src.sysmessage <def.itens_banked2>
src.dialogclose d_descricao1
findid.i_m_vendorfala.remove
src.CONSUMEGOLD=<LOCAL.PRECO>
src.bankself
src.p=<local.lastp>
src.region.flags=<local.flags>
src.update
endif

on=6
src.dialogclose d_descricao1
if (<tag.currentamount>==<uid.<tag.iuid<eval <tag.current>>>.amount>)
src.sysmessage <def.thatsall_tosell>
sdialog d_vendor_pod1
return 1
endif
if (<eval <tag.currentamount>+10> > <uid.<tag.iuid<eval <tag.current>>>.amount>)
src.sysmessage <def.thatsall_tosell>
sdialog d_vendor_pod1
return 1
endif
if (<tag.currentamount> > <uid.<tag.iuid<eval <tag.current>>>.amount>)
tag.currentamount=<uid.<tag.iuid<eval <tag.current>>>.amount>
sdialog d_vendor_pod1
else
tag.currentamount += 10
sdialog d_vendor_pod1
endif

on=7
if !(<tag.currentamount>)
src.sysmessage <def.cantbuy_minus10>
sdialog d_vendor_pod1
elif (<eval <tag.currentamount>-10> < 0)
src.sysmessage <def.cantbuy_minus10>
sdialog d_vendor_pod1
else
tag.currentamount -= 10
sdialog d_vendor_pod1
endif
src.dialogclose d_descricao1

on=8
src.dialogclose d_descricao1
IF !(<tag0.iuid<eval <tag.current>>>)
src.sysmessage <def.noitens_forsale_inslot>
sdialog d_vendor_pod1
return 1
endif
findid.i_bagdeitens.empty
serv.newdupe <tag.iuid<eval <tag.current>>>
new.attr=attr_move_never|attr_identified
new.cont=<findid.i_bagdeitens>
findid.i_bagdeitens.open
sdialog d_vendor_pod1

on=9
if !(strcmpi(<src.memoryfindtype.memory_gumprecord.tag.dialog_name>,d_descricao1))
src.dialogclose d_descricao1
sdialog d_vendor_pod1
tag.algarismo2
else
tag.algarismo2=<tag.current>
sdialog d_vendor_pod1
sdialog d_descricao1
endif

ON=10
tag.currentamount=0
tag.algarismo2=0
src.dialogclose d_descricao1
IF (<tag.current>==1)
tag.current=<tag.itensavenda>
sdialog d_vendor_pod1
else
tag.current -= 1
sdialog d_vendor_pod1
endif

on=11
src.dialogclose d_descricao1
If (<distance> > 5)
src.sysmessage <def.toofar>
findid.i_m_vendorfala.remove
return 1
endif
IF !(<tag0.iuid<eval <tag.current>+10>>)
tag.currentamount=0
tag.current=<tag.itensavenda>
sdialog d_vendor_pod1
else
tag.currentamount=0
tag.current += 10
sdialog d_vendor_pod1
endif

ON=12
tag.currentamount=0
tag.algarismo2=0
src.dialogclose d_descricao1
IF !(<tag0.iuid<eval <tag.current>-10>>)
tag.current=1
sdialog d_vendor_pod1
else
tag.current -= 10
sdialog d_vendor_pod1
endif

on=13
src.dialogclose d_descricao1
IF !(<tag0.iuid<eval <tag.current>>>)
src.sysmessage <def.noitens_forsale_inslot>
sdialog d_vendor_pod1
return 1
endif
findid.i_bagdeitens.empty
serv.newdupe <tag.iuid<eval <tag.current>>>
new.attr=attr_move_never
new.cont=<findid.i_bagdeitens.uid>
new.open
FORCONT <new> 999
attr=attr_move_never
IF (<type>==t_container)
open
ENDIF
ENDFOR
sdialog d_vendor_pod1

/////////////////////////////

[FUNCTION isbadstr]
IF !(<STRPOS 0 41 <ARGS>>=-1) || !(<EVAL (STRLEN(<ARGS>))>) || (STRMATCH(*[[]*,<ARGS>)) || (STRMATCH(*]*,<ARGS>)) || (STRMATCH(*[(]*,<ARGS>))) || (STRMATCH(*-*,<ARGS>)) || (STRMATCH(*[*}`>?´;:^%$¨!<#.@_~|=+&/æ{]*,<ARGS>))
RETURN 1
ELSE
RETURN 0
ENDIF

////////////////////////////

[itemdef i_bagdeitens]
NAME=bag de teste
ID=i_chest_metal
TYPE=t_container
WEIGHT=0

on=@create
color=07a0
attr=attr_move_never

/////////////////

[DIALOG d_descricao1]
0, 260
nomove
PAGE 0
resizepic 45 60 2620 210 205
dhtmlgump 51 70 197 185 1 200 Description of <uid.<tag.iuid<eval <tag.algarismo2>>>.name> : <QVAL <ISEMPTY <tag.desc<eval <tag.algarismo2>>>> ? ...:<tag.desc<eval <tag.algarismo2>>>>
button 245 54 00d8 00d8 1 0 1
dtext 247 51 88 -

[DIALOG d_descricao1 BUTTON]
ON=0
tag.algarismo2=0

ON=1
tag.algarismo2=0

///////////////////////

[DIALOG d_lastvendor]
210, 100
PAGE 0
resizepic 20 60 3500 310 300
dtext 100 70 035 <def.lastsells>
FOR 10
dtext 40 <eval (<local._for>*20)+80> 1152 <tag0.last<eval <local._for>>>
ENDFOR
button 242 321 4014 4015 1 0 1

[DIALOG d_lastvendor BUTTON]
on=0
findid.i_m_vendorfala.remove

on=1
sdialog d_caracteristicas_vendor

///////////////////////

[function f_ajeitar_itens]
local.itotal=<tag.itensavenda>
local.item=<eval <tag.current>+1>
FOR <eval <local.itotal>-<local.item>>
try tag.iuid<eval <local.item>>=<tag.iuid<eval <local.item>+1>>
try tag.desc<eval <local.item>>=<tag0.desc<eval <local.item>+1>>
IF (<ISEMPTY <TAG.DESC<eval <local.item>+1>>>)
try tag.desc<eval <local.item>>
ENDIF
try tag.preco<eval <local.item>>=<tag.preco<eval <local.item>+1>>
local.item += 1
ENDFOR
try tag.iuid<eval <local.itotal>>
try tag.desc<eval <local.itotal>>
try tag.preco<eval <local.itotal>>

//////////////////////////

[function f_retirar_itens]
IF (<tag0.itensavenda>)
local.itotais=<tag.itensavenda>
local.item=1
FOR <local.itotais>
try uid.<tag.iuid<eval <local.item>>>.tag.dispid2=
try uid.<tag.iuid<eval <local.item>>>.cont=<hval <argn1>>
try tag.iuid<eval <local.item>>
try tag.desc<eval <local.item>>
try tag.preco<eval <local.item>>
local.item += 1
ENDFOR
ENDIF

////////////////////////

[FUNCTION vendorcheck]
EVENTS e_vendorcheck
TRIGGER @TEST
LOCAL.TMP <NEW.TAG.RET>
REMOVE
RETURN <LOCAL.TMP>

[TYPEDEF e_vendorcheck]
ON=@TEST
LOCAL.LINK=<LINK>
FORCHARS 2
IF (<LOCAL.LINK>!=<UID>) && (<BASEID>==c_vendor_pod)
NEW.TAG.RET 1
RETURN 1
ENDIF
ENDFOR
NEW.TAG.RET 0

///////////////////////

[function f_players_perto]
IF (<findid.i_m_vendorfala.timer>==-1)
findid.i_m_vendorfala.remove
return 0
endif
IF !(<uid.<findid.i_m_vendorfala.tag.player>.isonline>)
findid.i_m_vendorfala.remove
return 0
endif
IF (<uid.<findid.i_m_vendorfala.tag.player>.distance> > 5)
try uid.<findid.i_m_vendorfala.tag.player>.sysmessage <def.toofar2>
findid.i_m_vendorfala.timer=0
serv.newitem=i_m_vendorfala
new.cont=<uid>
new.timer=<def.accessmenu_time>
new.tag.player=<src>
return 0
endif
IF (<findid.i_m_vendorfala>)
return 1
else
return 0
endif

////////////////////

[itemdef i_m_vendorfala]
name=vendor fala mem
id=i_memory
type=t_eq_script
layer=layer_special

on=@create
attr=attr_invis|attr_decay

on=@timer
try uid.<tag.player>.findid.i_memory_ivendor.remove
trysrc <tag.player> dialogclose d_vendor_owner_pod
trysrc <tag.player> dialogclose d_caracteristicas_vendor
trysrc <tag.player> dialogclose d_descricao1
trysrc <tag.player> dialogclose d_vendor_price1
trysrc <tag.player> dialogclose d_remover_item1
trysrc <tag.player> dialogclose d_vendor_pod1
trysrc <tag.player> dialogclose d_vendor_podprinc
trysrc <tag.player> dialogclose d_lastvendor
trysrc <tag.player> dialogclose d_mudarcor_vendor
trysrc <tag.player> dialogclose d_mudardesc1
trysrc <tag.player> dialogclose d_mudarnome_vendor
trysrc <tag.player> dialogclose d_mudarfala_vendor
remove
return 1

//////////////////

[itemdef i_m_falas]
name=memory falas
id=i_memory
layer=layer_special
type=t_eq_script
weight=0

on=@create
attr=attr_invis

///////////////////

[FUNCTION consumegold]
IF (<TOTALGOLD> < <ARGN1>)
RETURN 1
ENDIF
BOUNCE <FINDLAYER.layer_dragging>
LOCAL.AMOUNT=<ARGN1>
WHILE (<LOCAL.AMOUNT>)
IF (0<FINDLAYER.21.FINDID.i_gold>)
LOCAL.GOLD=<FINDLAYER.21.FINDID.i_gold.amount>
IF (<EVAL <LOCAL.AMOUNT>-<LOCAL.GOLD>> < 0)
FINDLAYER.21.FINDID.i_gold.AMOUNT -= <LOCAL.AMOUNT>
LOCAL.AMOUNT=0
ELSE
FINDLAYER.21.FINDID.i_gold.remove
LOCAL.AMOUNT -= <LOCAL.GOLD>
ENDIF
ELIF (0<FINDLAYER.29.FINDID.i_gold>)
LOCAL.GOLD=<FINDLAYER.29.FINDID.i_gold.amount>
IF (<EVAL <LOCAL.AMOUNT>-<LOCAL.GOLD>> < 0)
FINDLAYER.29.FINDID.i_gold.AMOUNT -= <LOCAL.AMOUNT>
LOCAL.AMOUNT=0
ELSE
FINDLAYER.29.FINDID.i_gold.remove
LOCAL.AMOUNT -= <LOCAL.GOLD>
ENDIF
ENDIF
ENDWHILE
UPDATE

/////////////////

[FUNCTION addgold]
LOCAL.AMOUNT=<ARGN1>
WHILE <LOCAL.AMOUNT>
IF (<LOCAL.AMOUNT> >= 65000)
SERV.NEWITEM i_gold
NEW.AMOUNT 65000
LOCAL.AMOUNT -= 65000
ELSE
SERV.NEWITEM i_gold
NEW.AMOUNT <LOCAL.AMOUNT>
LOCAL.AMOUNT=0
ENDIF
IF (0<ARGN2>)
NEW.CONT <ARGN2>
ELSE
BOUNCE <NEW>
ENDIF
ENDWHILE
UPDATE

[PLEVEL 4]
addgold

/////////////////

[FUNCTION rtime.year]
return <strsub 0 4 <serv.rtime>>

[FUNCTION rtime.month]
return <strsub 5 2 <serv.rtime>>

[FUNCTION rtime.day]
return <strsub 8 2 <serv.rtime>>

[function totalgold]
RETURN <eval <rescount <def.vendorsys_money>>+<findlayer.29.rescount <def.vendorsys_money>>>

[DEFNAME gump_things]
memory_gumprecord 08000

[EOF]