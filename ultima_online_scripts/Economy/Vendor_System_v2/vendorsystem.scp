// Ron Custer
// Aug 31 2002
// version .1

[ITEMDEF i_vendor_rental]
ID=0bcf
DUPELIST=0bd0,0bcf
NAME=Vendor Rental
TYPE=t_script
CATEGORY=Added 
SUBSECTION=Interactive 
DESCRIPTION=Vendor Rental Sign 

ON=@CREATE
COLOR=000d1
TAG.RENTED=0
TAG.OWNER=0
ATTR=attr_decay

ON=@DCLICK
SRC.ACT=<UID>
IF !(<eval <SRC.ACT.TAG.RENTED>>)
      SRC.MENU m_vendor_intro
      RETURN 1
ELSE
   IF (<SRC.ACT.LINK.TAG.OWNER>==<SRC.UID>)
      SRC.MENU m_vendor_admin
      RETURN 1
   ELSE
   SRC.SYSMESSAGE The shop is already rented.
   RETURN 1
   ENDIF
ENDIF
RETURN 1

ON=@TIMER
LINK.REMOVE
LINK=04fffffff
TAG.RENTED=0
TAG.OWNER=0

// GUMPS 

[MENU m_vendor_intro]
Vendor Rental System

ON=0 Rent a vendor for this shop
IF (<eval <SRC.BANKBALANCE>+<SRC.FINDLAYER(21).FINDTYPE(i_GOLD).AMOUNT>> >=15000)
SRC.ACT.TAG.RENTED=1
SRC.ACT.TAG.OWNER=<SRC.UID>
SRC.ACT.TIMER=60*60*24*7
VAR.SIGN=<SRC.ACT>
SRC.NEWNPC { C_RENTAL_VENDOR 1 C_RENTAL_VENDOR_F 1 }
VAR.VEND=<SRC.ACT.UID> // keep the vendors uid for later use.
SRC.ACT.TAG.OWNER=<SRC.UID>
SRC.ACT=<VAR.SIGN>
SRC.ACT.LINK=<VAR.VEND>

SRC.ACT=<VAR.VEND>
SRC.NEWITEM=i_memory
SRC.ACT.COLOR=memory_ipet
SRC.ACT.LINK=<SRC.UID>
SRC.ACT.LAYER=layer_special
SRC.ACT.CONT=<VAR.VEND>
SRC.CONSUMEBANK 15000 i_gold
SRC.UPDATE 
RETURN 1
ELSE
SRC.SAY You do not have the funds to rent a vendor
ENDIF

[MENU m_vendor_admin]
Rented Vendor Administration Menu

ON=0 Change my vendor's name
SRC.DIALOG d_name_vendor

ON=0 Pay for another week (<eval <SRC.ACT.TIMER>/60> minutes left)
IF (<eval <SRC.BANKBALANCE>+<SRC.FINDLAYER(21).FINDTYPE(i_GOLD).AMOUNT>> >=15000)
    SRC.CONSUMEBANK 15000 i_gold
    SRC.UPDATE
    SRC.ACT.TIMER=60*60*24*7
    SRC.ACT.LINK.SAY I shall sell for you for another 7 days!
ELSE
    SRC.SYSMESSAGE You do not have the proper funds to cover another week
ENDIF


[DIALOG d_name_vendor]
0,0
nomove
resizepic 190 175 5054 335 150
resizepic 200 185 3000 315 130
resizepic 220 235 3000 270 22
text 215 200 995 0
text 255 275 0000 1
button 220 275 4005 4006 1 0 1
text 435 275 0000 2
button 400 275 4005 4006 1 0 0
textentry 225 237 253 25 0 0 3

[DIALOG d_name_vendor TEXT]
How would you like to name the vendor?
Continue
Cancel
<src.act.link.name>

[DIALOG d_name_vendor BUTTON]
ON=0
ON=1
src.act.link.say My new name is <ARGTXT[0]>!
SRC.ACT.LINK.NAME <ARGTXT[0]>
RETURN 1

// NPCS - intended for future expansion
[CHARDEF C_RENTAL_VENDOR
NAME=Rented Vendor
ID=C_MAN
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff

TSPEECH=spk_human_prime
TSPEECH=spk_shopkeep
TSPEECH=spk_needs
TSPEECH=spk_human_default

TEVENTS=e_Human_HearUnk
TEVENTS=e_Human_ConvInit
TEVENTS=e_Human_Needs
TEVENTS=e_Human_Greet
TEVENTS=e_Human_Space
TEVENTS=e_Human_Refuse
TEVENTS=e_Human_Environ

CATEGORY=Civilized
SUBSECTION=Tradesmen
DESCRIPTION=Vendor

ON=@Create
NPC=brain_vendor
COLOR=colors_skin
FLAGS=028000005
NEED=i_gold

STR={66 80}
DEX=90
INT={66 80}

FENCING={15.0 38.0}
PARRYING={45.0 68.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={45.0 68.0}
TACTICS={45.0 68.0}

ITEMNEWBIE=i_lantern
ITEMNEWBIE=random_male_hair
COLOR=colors_hair
ITEMNEWBIE=random_facial_hair
COLOR=match_hair

ITEMNEWBIE=i_shirt_plain
COLOR=colors_all
ITEMNEWBIE=random_pants
COLOR=colors_all
ITEMNEWBIE=random_shoes
COLOR=colors_neutral

ON=@CLICK
SAYUA 268 0 0 0 <NAME>
RETURN 1

[CHARDEF C_RENTAL_VENDOR_F]
NAME=Rented Vendor
ID=C_WOMAN
CAN=0
FOODTYPE=

TSPEECH=spk_human_prime
TSPEECH=spk_shopkeep
TSPEECH=spk_needs
TSPEECH=spk_human_default

TEVENTS=e_Human_HearUnk
TEVENTS=e_Human_ConvInit
TEVENTS=e_Human_Needs
TEVENTS=e_Human_Greet
TEVENTS=e_Human_Space
TEVENTS=e_Human_Refuse
TEVENTS=e_Human_Environ

ON=@Create
NPC=brain_vendor
COLOR=colors_skin
FLAGS=028000001
NEED=i_gold

STR={66 80}
DEX=90
INT={66 80}
FENCING={15.0 38.0}
PARRYING={45.0 68.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={45.0 68.0}
TACTICS={45.0 68.0}


ITEMNEWBIE=i_lantern
ITEMNEWBIE=random_female_hair
COLOR=colors_hair

ON=@NPCRestock
ITEM=i_shirt_plain
COLOR=colors_all
ITEM=random_skirt
COLOR=colors_all
ITEM=random_shoes
COLOR=colors_neutral
ITEM=random_coin_purse

ON=@CLICK
SAYUA 268 0 0 0 <NAME>
RETURN 1
