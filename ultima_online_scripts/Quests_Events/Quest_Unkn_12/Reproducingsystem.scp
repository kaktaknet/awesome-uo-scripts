//Scripted by <GM_Ramon>, please leave credit
//Bugs to waghk@hotmail.com
//FIRST OFF, THE CHOCOBOS
[CHARDEF c_chocobo_different]
ID=c_ostard_desert
NAME=Strange chocobo
DAM=9,11
TEVENTS=e_trainable

ON=@CREATE
COLOR=0499
STR={10 140}
DEX={10 120}
INT=1
EVENTS=e_malefemale
NPC=brain_animal
TAG.MALE=<EVAL {1 2}>
TACTICS={5.0 150.0}
WRESTLING={5.0 150.0}
PARRYING={5.0 150.0}
MAGICRESISTANCE={5.0 150.0}
TAMING={5.0 100.0}
FAME={0 100}
KARMA={-100 100}

[CHARDEF c_chocobo_yellow]
ID=c_ostard_desert
NAME=Yellow chocobo
DAM=6,11

ON=@CREATE
COLOR=037
STR={80 90}
DEX={40 60}
INT=1
EVENTS=e_malefemale
NPC=brain_animal
TAG.MALE=<EVAL {1 2}>
TACTICS={40.0 30.0}
WRESTLING={40.0 30.0}
PARRYING={40.0 30.0}
MAGICRESISTANCE={40.0 50.0}
TAMING=50.0
FAME={0 100}
KARMA={-100 100}

[CHARDEF c_chocobo_blue]
ID=c_ostard_desert
NAME=Blue chocobo
DAM=8,13
TEVENTS=e_trainable

ON=@CREATE
COLOR=0796
STR={90 100}
DEX={50 70}
INT=1
EVENTS=e_malefemale
NPC=brain_animal
TAG.MALE=<EVAL {1 2}>
TACTICS={50.0 60.0}
WRESTLING={50.0 60.0}
PARRYING={50.0 60.0}
MAGICRESISTANCE={50.0 60.0}
TAMING=60.0
FAME={0 200}
KARMA={-200 200}

[CHARDEF c_chocobo_green]
ID=c_ostard_desert
NAME=Green chocobo
DAM=7,13

CATEGORY=Chocobo
SUBSECTION=rides
DESCRIPTION=green

ON=@CREATE
COLOR=00851
STR={85 95}
DEX={45 65}
INT=1
EVENTS=e_malefemale
NPC=brain_animal
TAG.MALE=<EVAL {1 2}>
TACTICS={40.0 30.0}
WRESTLING={40.0 30.0}
PARRYING={40.0 30.0}
MAGICRESISTANCE={40.0 50.0}
TAMING=50.0
FAME={0 100}
KARMA={-100 100}

[CHARDEF c_chocobo_red]
ID=c_ostard_desert
NAME=Red chocobo
DAM=10,15
TEVENTS=e_trainable

ON=@CREATE
COLOR=021
STR={100 110}
DEX={60 80}
INT=1
EVENTS=e_malefemale
NPC=brain_animal
TAG.MALE=<EVAL {1 2}>
TACTICS={60.0 70.0}
WRESTLING={60.0 70.0}
PARRYING={60.0 70.0}
MAGICRESISTANCE={60.0 70.0}
TAMING=700.0
FAME={0 500}
KARMA={-100 500}

[CHARDEF c_chocobo_black]
ID=c_ostard_desert
NAME=Black chocobo
DAM=11,22
TEVENTS=e_antimagic
TEVENTS=e_trainable

ON=@CREATE
COLOR=07a2
STR={120 140}
DEX={80 100}
INT=1
EVENTS=e_malefemale
NPC=brain_animal
TAG.MALE=<EVAL {1 2}>
TACTICS={80.0 90.0}
WRESTLING={80.0 90.0}
PARRYING={80.0 90.0}
MAGICRESISTANCE={80.0 90.0}
TAMING=80.0
FAME={0 1000}
KARMA={-100 1000}

[CHARDEF c_chocobo_golden]
ID=c_ostard_desert
NAME=Golden chocobo
CAN=MT_WALK|MT_RUN|MT_FLY
DAM=18,24
TEVENTS=e_antimagic
TEVENTS=e_trainable

ON=@CREATE
COLOR=06f1
STR={150 180}
DEX={90 120}
INT=1
EVENTS=e_malefemale
NPC=brain_animal
TAG.MALE=<EVAL {1 2}>
TACTICS=100.0
WRESTLING=100.0
PARRYING=100.0
MAGICRESISTANCE=100.0
TAMING=100.0
FAME={0 7000}
KARMA={-100 10000}


//Just a tag-based event to see the sex(so name cannot be changed)
[EVENTS e_malefemale]
ON=@CLICK
IF !(<FINDID.i_memory_oldnesschoco>)
SRC.NEWITEM i_memory_oldnesschoco
SRC.ACT.CONT <UID>
SRC.ACT.TIMER=10
ENDIF
IF (0<TAG.MALE>==1)
VAR.GENDER=male
ELSE
VAR.GENDER=female
ENDIF
MESSAGE [<VAR.GENDER>]

//------------Items to breed--------------
[ITEMDEF i_nut_simple]
ID=0c5d
VALUE=50
NAME=Simple nut
TYPE=t_chocobo_nut

ON=@CREATE
MORE2=1
COLOR=0885

[ITEMDEF i_nut_mapa]
ID=0c5d
VALUE=200
NAME=Mapa nut
TYPE=t_chocobo_nut

ON=@CREATE
MORE2=3
COLOR=0885

[ITEMDEF i_nut_kupo]
ID=0c5d
VALUE=500
NAME=Kupo nut
TYPE=t_chocobo_nut

ON=@CREATE
MORE2=6
COLOR=0885

[ITEMDEF i_nut_great_kupo]
ID=0c5d
VALUE=1000
NAME=Great kupo tree nut
TYPE=t_chocobo_nut

ON=@CREATE
MORE2=10
COLOR=0885

[ITEMDEF i_nut_goddess_Ghalena]
ID=0c5d
VALUE=10000
NAME=Godess Ghalena nut
TYPE=t_chocobo_nut

ON=@CREATE
MORE2=20
COLOR=0855

//The type of the nuts
[TYPEDEF t_chocobo_nut]
ON=@DCLICK
TARGET Select a chocobo
RETURN 1

ON=@TARGON_CHAR
IF (<SRC.TARG.MEMORYFINDTYPE.memory_ipet.LINK>==<SRC.UID>)
IF (<SRC.TARG.BODY>==c_ostard_desert)&&(<SRC.TARG.ISEVENT.e_malefemale>)
IF !(<SRC.TARG.FINDID.i_reproducing>)
	SRC.NEWITEM i_reproducing
	SRC.ACT.CONT=<SRC.TARG.UID>
	SRC.ACT.TIMER=60
	SRC.NEWITEM i_breeder_new
	SRC.ACT.LINK=<SRC.TARG.UID>
	SRC.ACT.MORE2=<MORE2>
	SRC.ACT.MORE=<SRC.TARG.UID>
	SRC.ACT.CONT=<SRC.UID>	
	SRC.ACT.DCLICK
ELSE
SRC.MESSAGE She's already reproducing herself!
ENDIF
ENDIF
CONSUME
ELSE
SRC.MESSAGE That chocobo ain't yours!
ENDIF
RETURN 1


//The main thing to breed
[ITEMDEF i_breeder_new]
ID=i_memory
NAME=I'm reproducing things man, ain't this greattt???
TYPE=t_eq_script

ON=@DCLICK
TARGET Choose another chocobo
TIMER=20
RETURN 1

ON=@TARGON_CHAR
IF (<SRC.TARG.TAG.MALE>==<LINK.TAG.MALE>)
SRC.MESSAGE They have the same gender degenerated!
REMOVE
RETURN 1
ELSEIF (<SRC.TARG.FINDID.i_reproducing>)
SRC.MESSAGE She's already reproducing herself!
REMOVE
RETURN 1
ENDIF
IF !(<SRC.TARG.MEMORYFINDTYPE.memory_ipet.LINK>==<SRC.UID>)
SRC.MESSAGE That chocobo ain't yours!
REMOVE
RETURN 1
ENDIF
IF (<SRC.TARG.BODY>==c_ostard_desert)&&(<SRC.TARG.ISEVENT.e_malefemale>)
	SRC.SYSMESSAGE Ok, wait until nature does it's work.
	IF (<SRC.TARG.ID>==c_chocobo_yellow) 
	CONT.TAG.TYPENAEW=2
	ELSEIF (<SRC.TARG.ID>==c_chocobo_blue)
	CONT.TAG.TYPENAEW=4
	ELSEIF (<SRC.TARG.ID>==c_chocobo_red)
	CONT.TAG.TYPENAEW=8
	ELSEIF (<SRC.TARG.ID>==c_chocobo_black)
	CONT.TAG.TYPENAEW=16
	ELSEIF (<SRC.TARG.ID>==c_chocobo_golden)
	CONT.TAG.TYPENAEW=32
	ELSEIF (<SRC.TARG.ID>==c_chocobo_different)
	CONT.TAG.TYPENAEW=<EVAL {1 40}>
	ENDIF
	CONT.TAG.MYFATHER=<SRC.TARG.UID>
	CONT.TAG.FATHERSTR=<SRC.TARG.STR>
	CONT.TAG.FATHERDEX=<SRC.TARG.DEX>
	CONT.TAG.FATHERTACTICS=<SRC.TARG.TACTICS>
	CONT.TAG.FATHERWRESTLING=<SRC.TARG.WRESTLING>
	CONT.TAG.FATHERMRESISTANCE=<SRC.TARG.MAGICRESISTANCE>
	CONT.TAG.FATHERPARRYING=<SRC.TARG.PARRYING>
	TIMER=<EVAL {30 60}>
SRC.NEWITEM i_reproducing
SRC.ACT.CONT=<SRC.TARG.UID>
ENDIF
RETURN 1		

ON=@TIMER
IF (0<CONT.TAG.FATHERSTR>)
f_check_all_the_small_things
f_apply_genetics_and_close_the_paradeta
ENDIF
REMOVE
RETURN 1

//The main function to breed
[FUNCTION f_check_all_the_small_things]
VAR.NUTPOWER=<EVAL <MORE2>*2>
IF (<LINK.ID>==c_chocobo_yellow)
VAR.TYPENEW2=2
ELSEIF (<LINK.ID>==c_chocobo_blue)
VAR.TYPENEW2=4
ELSEIF (<LINK.ID>==c_chocobo_red)
VAR.TYPENEW2=8
ELSEIF (<LINK.ID>==c_chocobo_black)
VAR.TYPENEW2=16
ELSEIF (<LINK.ID>==c_chocobo_golden)
VAR.TYPENEW2=32
ELSEIF (<LINK.ID>==c_chocobo_different)
VAR.TYPENEW2=<EVAL {1 40}>
ENDIF
VAR.TYPEFINAL=<EVAL <evaL <VAR.TYPENEW2>>+<EVAL <CONT.TAG.TYPENAEW>>+(-<EVAL {0 2}>)+<EVAL {0 <VAR.NUTPOWER>}>>
IF (<CONT.TAG.DEBUG.COMBAT>)
CONT.MESSAGE New type: <EVAL <VAR.tYPEFINAL>>; Mother's try: <EVAL <VAR.TYPENEW2>>, Father's try: <EVAL <CONT.TAG.TYPENAEW>> NUT: <EVAL <VAR.NUTPOWER>>!
ENDIF
IF (<VAR.TYPEFINAL> < 0)
VAR.TYPEFINAL=0
ENDIF
IF (RAND(100)==0) //Truly strange newborn!
CONT.NEWNPC c_chocobo_different
CONT.ACT.TAG.MALE=<EVAL {1 2}>
CONT.ACT.TAG.SUBEPLUS=<EVAL {-2 5}>
CONT.ACT.P=<LINK.P>
ELSEIF (<VAR.TYPEFINAL> <= 6)
CONT.NEWNPC c_chocobo_yellow
CONT.ACT.TAG.MALE=<EVAL {1 2}>
CONT.ACT.TAG.SUBEPLUS=0
CONT.ACT.P=<LINK.P>
ELSEIF (<VAR.TYPEFINAL> <= 12)
CONT.NEWNPC c_chocobo_blue
CONT.ACT.TAG.MALE=<EVAL {1 2}>
CONT.ACT.TAG.SUBEPLUS=<EVAL {0 1}>
CONT.ACT.P=<LINK.P>
ELSEIF (<VAR.TYPEFINAL> <= 24)
CONT.NEWNPC c_chocobo_red
CONT.ACT.TAG.MALE=<EVAL {1 2}>
CONT.ACT.TAG.SUBEPLUS=<EVAL {0 2}>
CONT.ACT.P=<LINK.P>
ELSEIF (<VAR.TYPEFINAL> <= 35)
CONT.NEWNPC c_chocobo_green
CONT.ACT.TAG.MALE=<EVAL {1 2}>
CONT.ACT.TAG.SUBEPLUS=<EVAL {0 3}>
CONT.ACT.P=<LINK.P>
ELSEIF (<VAR.TYPEFINAL> <= 60)
CONT.NEWNPC c_chocobo_black
CONT.ACT.TAG.MALE=<EVAL {1 2}>
CONT.ACT.TAG.SUBEPLUS=<EVAL {1 3}>
CONT.ACT.P=<LINK.P>
ELSE
CONT.NEWNPC c_chocobo_golden
CONT.ACT.TAG.MALE=<EVAL {1 2}>
CONT.ACT.TAG.SUBEPLUS=<EVAL {1 4}>
CONT.ACT.P=<LINK.P>
ENDIF
CONT.SYSMESSAGE A NEW CHOCOBO HAS BEEN BORN!
IF (RAND(15)==0)
	IF (0<UID.<MORE>.TAG.MALE>==2)
		UID.<MORE>.TAG.MALE.SUICIDE
	ELSE
		LINK.SUICIDE
	ENDIF
CONT.SYSMESSAGE But the mother died during the birth!
ENDIF
LINK.ANIM 3
CONT.ACT.MOVE s
VAR.NEWBORNID=<CONT.ACT.UID>

//Optional function to set the new skills and str of the chocobo.
[FUNCTION f_apply_genetics_and_close_the_paradeta]
VAR.STRGAIN=<EVAL (<EVAL <CONT.TAG.FATHERSTR>+<LINK.STR>+<EVAL {0 50}>+(-<EVAL {0 150}>)+(<VAR.NUTPOWER>)>/2>
VAR.DEXGAIN=<EVAL (<EVAL <CONT.TAG.FATHERDEX>+<LINK.DEX>+<EVAL {0 50}>+(-<EVAL {0 150}>)+(<VAR.NUTPOWER>)>/2>
VAR.TACTICSGAIN=<EVAL {50 300}>+<EVAL <VAR.NUTPOWER>*10>
VAR.WRESTLINGGAIN=<EVAL {50 300}>+<EVAL <VAR.NUTPOWER>*10>
VAR.MRESISTANCEGAIN=<EVAL {50 300}>+<EVAL <VAR.NUTPOWER>*10>
VAR.PARRYINGGAIN=<EVAL {50 300}>+<EVAL <VAR.NUTPOWER>*10>
IF (<VAR.STRGAIN> < 40)
VAR.STRGAIN=40
ENDIF
IF (<VAR.DEXGAIN> < 40)
VAR.DEXGAIN=40
ENDIF
TRY UID.<CONT.TAG.MYFATHER>.FINDID.I_reproducing.REMOVE
LINK.FINDID.I_reproducing.REMOVE
CONT.ACT.STR=<VAR.STRGAIN>
CONT.ACT.DEX=<VAR.DEXGAIN>
CONT.ACT.TACTICS=<VAR.TACTICSGAIN>
CONT.ACT.WRESTLING=<VAR.WRESTLINGGAIN>
CONT.ACT.MAGICRESISTANCE=<VAR.MRESISTANCEGAIN>
CONT.ACT.PARRYING=<VAR.PARRYINGGAIN>
CONT.TAG.FATHERSTR=
CONT.TAG.FATHERDEX=
CONT.TAG.FATHERTACTICS=
CONT.TAG.TYPENAEW=
CONT.TAG.FATHERWRESTLING=
CONT.TAG.FATHERMRESISTANCE=
CONT.TAG.FATHERPARRYING=
CONT.TAG.MYFATHER=
CONT.NEWITEM i_memory
CONT.ACT.COLOR memory_ipet
CONT.ACT.MORE1 04
CONT.ACT.MORE <SERV.TIME>
CONT.ACT.MOREP <LINK.P>
CONT.ACT.LINK <CONT.UID>
CONT.ACT.CONT <VAR.NEWBORNID>
CONT.NEWITEM i_memory_oldnesschoco
CONT.ACT.TIMER=<EVAL {60*60 6*60*60}>
CONT.ACT.CONT <VAR.NEWBORNID>
IF (<CONT.TAG.DEBUG.COMBAT>)
CONT.MESSAGE New Chocobo: STR: <EVAL <VAR.STRGAIN>>, DEX: <EVAL <VAR.DEXGAIN>>,TACTICS: <EVAL <VAR.TACTICSGAIN>>,WRESTLING: <EVAL <VAR.WRESTLINGGAIN>>,MAGICRESIST: <EVAL <VAR.MRESISTANCEGAIN>> and PARRYING: <EVAL <VAR.PARRYINGGAIN>>
ENDIF

[EVENTS e_trainable]
ON=@HIT
IF (<MEMORYFINDTYPE.memory_ipet>)&&!(<FLAGS>&04000000)
	VAR.QUESUBO=<EVAL {0 4}>

IF (<VAR.QUESUBO>==0)
	IF (RAND(7)==<VAR.QUESUBO>)&&(<STR> < 350)
	STR=<STR>+<EVAL {1 3}>+(<EVAL 0<TAG.SUBEPLUS>>)
	SAY *<NAME> is now stronger!!!*
	ENDIF
ELSEIF (<VAR.QUESUBO>==1)
	IF (RAND(7)==<VAR.QUESUBO>)&&(<DEX> < 200)
	DEX=<DEX>+<EVAL {1 3}>+(<EVAL 0<TAG.SUBEPLUS>>)
	SAY *<NAME> is now more agile!!!*	
	ENDIF
ELSEIF (<VAR.QUESUBO>==2)
	IF (RAND(7)==<VAR.QUESUBO>)&&(<TACTICS> < 100.0)
	TACTICS=<TACTICS>+0.5+(<EVAL 0<TAG.SUBEPLUS>>)
	SAY *<NAME> has better war tactics!!!*
	ENDIF
ELSEIF (<VAR.QUESUBO>==3)
	IF (RAND(7)==<VAR.QUESUBO>)&&(<PARRYING> < 100.0)
	PARRYING=<PARRYING>+0.5+(<EVAL 0<TAG.SUBEPLUS>>)
	SAY *<NAME> blocks better now!!!*
	ENDIF
ELSEIF (<VAR.QUESUBO>==4)
	IF (RAND(7)==<VAR.QUESUBO>)&&(<WRESTLING> < 100.0)
	WRESTLING=<WRESTLING>+0.5+(<EVAL 0<TAG.SUBEPLUS>>)
	SAY *<NAME> fights better now!!!*
	ENDIF
ENDIF
IF (<TACTICS> > 100.0)
TACTICS=100.0
ENDIF
IF (<WRESTLING> > 100.0)
TACTICS=100.0
ENDIF
IF (<PARRYING> > 100.0)
TACTICS=100.0
ENDIF
ENDIF

[EVENTS e_antimagic]
ON=@SPELLEFFECT
IF !(<SRC.UID>==<MEMORYFINDTYPE.memory_ipet.LINK>)
IF (<ARGN>==1)||(<ARGN>==3)||(<ARGN>==5)||(<ARGN>==8)||(<ARGN>==12)||(<ARGN>==18)||(<ARGN>==20)||(<ARGN>==27)||(<ARGN>==28)||(<ARGN>==30)||(<ARGN>==31)||(<ARGN>==37)||(<ARGN>==39)||(<ARGN>==42)||(<ARGN>==43)||(<ARGN>==46)||(<ARGN>==47)||(<ARGN>==49)||(<ARGN>==51)||(<ARGN>==53)||(<ARGN>==55)||(<ARGN>==57)||(<ARGN>==69)||(<ARGN>==70)
		IF (RAND(7)==1)&&(<MAGICRESISTANCE> < 125.0)
		MAGICRESISTANCE=<MAGICRESISTANCE>+0.5
		SAY *<NAME> is more used now to magic!*
		ENDIF
	IF (RAND(189) < <EVAL <MAGICRESISTANCE>/10>)
		SYSMESSAGE *the spell has no effect on <NAME>!*
	RETURN 1
	ENDIF	
ENDIF
ENDIF

[ITEMDEF i_reproducing]
ID=i_memory
TYPE=t_eq_script
NAME=Security

ON=@TIMER
REMOVE
RETURN 1

[ITEMDEF i_memory_oldnesschoco]
ID=i_memory
TYPE=t_eq_script
NAME=My age

ON=@TIMER
MORE=<MORE>+1
CONT.SAY I'm getting older!
CONT.STR=<CONT.STR>+<EVAL {2 8}>
CONT.DEX=<CONT.DEX>+<EVAL {1 5}>
IF (<MORE> > <EVAL {10 25}>   //May die young... or old
	IF !(<CONT.ID>==c_chocobo_golden)||(<CONT.ID>==c_chocobo_different)
		CONT.SUICIDE
	ENDIF
ENDIF
TIMER=<EVAL {60*60 4*60*60}>
RETURN 1
