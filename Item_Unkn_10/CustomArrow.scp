/////////////////////////////////////////////////////////////////
// special bow system to allow for special arrows
// version 1.0, written for sphere 55i
//
// Created by Belgar of 'A Virtual Paradise'. A free, player run, shard      
// This file is free for use on other free shards
//
// to install: copy the contents in a file called vp_bow_system.scp
// make sure this file is loaded and resync your server 
//                                                                           
// Please do not repost this script without my permission                    
// If you modify this file, please leave this header intact                  
/////////////////////////////////////////////////////////////////

//------ usable items -------
// i_bow : this is the only bow actually needed, do not add the special bows directly
//     the special bows will break when used !!

//-------- overwrite for normal bow---------------
[ITEMDEF 013b1]
DEFNAME=i_bow
TYPE=T_WEAPON_BOW
FLIP=1
SKILLMAKE=17.8 BOWCRAFT
RESOURCES=2 I_LOG
RESOURCES2=2 I_LOG, i_cat_gut
WEIGHT=5
DAM=9,21
//SPEED=20
SKILL=Archery
REQSTR=20
TWOHANDS=Y
CATEGORY=Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=Bow
DUPELIST=013b2

TDATA3=i_arrow
TDATA4=i_arrow_x

ON=@Create
HITPOINTS={31 60}

on=@equip
src.events +bowequiped //load special arrow events
//src.correctammo //make sure the correct ammo is loaded
src.newitem bowremover
src.act.link=<src.uid>
src.act.p=<src.p>
src.act.timer 1

on=@unequip
src.events -bowequiped //remove event when not needed anymore

//-------- overwrite normal arrow --------------

[ITEMDEF 0f3f]
//Arrow
DEFNAME=i_arrow
NAME=arrow%s
TYPE=T_WEAPON_ARROW
FLIP=1
RESOURCES=1 i_arrow_shaft, 1 i_feather
REPLICATE=1
SKILLMAKE=1.0 BOWCRAFT
WEIGHT=0.1
CATEGORY=Items by Professions
SUBSECTION=Archers/Bowyers
DESCRIPTION=Arrow
DUPELIST=0f40,0f41,0f42

on=@dclick
if (<src.findlayer.2.dispid>==i_bow)
if (<topobj.uid>==<src.uid>)
p=<src.p>
cont=<src.findlayer.21.uid>
src.correctammo
return 1
endif
endif

//------------- event section ------------------

[events bowequiped] //the main event

on=@hittry //preparing to fire bow

if (<findtype.t_weapon_arrow.uid>) //any arrows in backpack ??
correctammo
endif

//--------------- arrows section --------------------

[itemdef i_ice_arrow]
id=i_arrow
name=ice arrow%s

on=@create
color=03

on=@dclick
if (<src.findlayer.2.dispid>==i_bow)
if (<topobj.uid>==<src.uid>)
p=<src.p>
cont=<src.findlayer.21.uid>
src.correctammo
return 1
endif
endif

[itemdef i_fire_arrow]
id=i_arrow
name=fire arrow%s

on=@create
color=030

on=@dclick
if (<src.findlayer.2.dispid>==i_bow)
if (<topobj.uid>==<src.uid>)
p=<src.p>
cont=<src.findlayer.21.uid>
src.correctammo
return 1
endif
endif

[itemdef i_dragon_arrow]
id=i_arrow
name=arrow%s of dragon slaying

on=@create
color=0795

on=@dclick
if (<src.findlayer.2.dispid>==i_bow)
if (<topobj.uid>==<src.uid>)
p=<src.p>
cont=<src.findlayer.21.uid>
src.correctammo
return 1
endif
endif

on=@pickup_pack
if !(<src.skillclass>==class_ranger)
src.message you break the arrows because of your rough handling
remove
return 1
endif

on=@pickup_ground
if !(<src.skillclass>==class_ranger)
src.message you break the arrows because of your rough handling
remove
return 1
endif

[itemdef i_silver_arrow]
id=i_arrow
name=silver arrow%s

on=@create
color=038a

on=@dclick
if (<src.findlayer.2.dispid>==i_bow)
if (<topobj.uid>==<src.uid>)
p=<src.p>
cont=<src.findlayer.21.uid>
src.correctammo
return 1
endif
endif

on=@pickup_pack
if !(<src.skillclass>==class_ranger)
src.message you break the arrows because of your rough handling
remove
return 1
endif

on=@pickup_ground
if !(<src.skillclass>==class_ranger)
src.message you break the arrows because of your rough handling
remove
return 1
endif

[itemdef i_speed_arrow]
id=i_arrow
name=arrow%s of dexterity
value=10
SKILLMAKE=bowcraft 70.0
resources=i_log, i_feather, I_scroll_agility

on=@create
color=07a5

on=@dclick
if (<src.findlayer.2.dispid>==i_bow)
if (<topobj.uid>==<src.uid>)
p=<src.p>
cont=<src.findlayer.21.uid>
src.correctammo
return 1
endif
endif

on=@pickup_pack
if !(<src.skillclass>==class_ranger)
src.message you break the arrows because of your rough handling
remove
return 1
endif

on=@pickup_ground
if !(<src.skillclass>==class_ranger)
src.message you break the arrows because of your rough handling
remove
return 1
endif


//-------------- special bow section -----------------

[itemdef i_ice_bow] // do not add this bow directly, add i_bow
id=i_bow
tdata3=i_ice_arrow
tdata4=03818

on=@equip
src.events +bowequiped
//src.correctammo
src.newitem bowremover
src.act.link=<src.uid>
src.act.p=<src.p>
src.act.timer 1


on=@unequip
src.events -bowequiped

on=@damage
src.hits=(<src.hits> - {1 4})
if !(rand(10))
src.newitem i_paratry
src.act.link=<src.uid>
src.act.timer=1
src.act.equip
endif

[itemdef i_fire_bow] // do not add this bow directly, add i_bow
id=i_bow
tdata3=i_fire_arrow
tdata4=036e4

on=@equip
src.events +bowequiped
//src.correctammo
src.newitem bowremover
src.act.link=<src.uid>
src.act.p=<src.p>
src.act.timer 1

on=@unequip
src.events -bowequiped

on=@damage
src.hits=(<src.hits> - {4 9})

[itemdef i_dragon_bow] // do not add this bow directly, add i_bow
id=i_bow
tdata3=i_dragon_arrow

on=@equip
src.events +bowequiped
src.newitem bowremover
src.act.link=<src.uid>
src.act.p=<src.p>
src.act.timer 1

on=@unequip
src.events -bowequiped

on=@damage
if (<src.npc>==13)
src.hits=(<src.hits>-<argn>)
endif

[itemdef i_silver_bow] // do not add this bow directly, add i_bow
id=i_bow
tdata3=i_silver_arrow

on=@equip
src.events +bowequiped
src.newitem bowremover
src.act.link=<src.uid>
src.act.p=<src.p>
src.act.timer 1

on=@unequip
src.events -bowequiped

on=@damage
if (<src.npc>==12)
src.damage <argn>
endif

[itemdef i_speed_bow] // do not add this bow directly, add i_bow
id=i_bow
tdata3=i_speed_arrow
dam=9,15
weight=2.5

on=@equip
src.events +bowequiped
src.newitem bowremover
src.act.link=<src.uid>
src.act.p=<src.p>
src.act.timer 1

on=@unequip
src.events -bowequiped


//----------- functions section -----------------

[function bowcopy] //replace current bow with this bow

var.more1=<findlayer.2.more1>
var.more2=<findlayer.2.more2>
var.morep=<findlayer.2.morep>
var.attr=<findlayer.2.attr>
var.color=<findlayer.2.color>
findlayer.2.remove 
newitem <args>
act.more1=<var.more1>
act.more2=<var.more2>
act.morep=<var.morep>
act.attr=<var.attr>
act.color=<var.color>
act.cont=<uid>
events +bowequiped

[function correctammo] //make sure the bow equiped can fire current ammo

if (<findtype.t_weapon_arrow.baseid>== i_ice_arrow) && !(<findlayer.2.baseid>==i_ice_bow) 
bowcopy i_ice_bow
emote load ice arrows 
return 1

elseif (<findtype.t_weapon_arrow.baseid>== i_fire_arrow) && !(<findlayer.2.baseid>==i_fire_bow)
bowcopy i_fire_bow
emote load fire arrows
return 1

elseif (<findtype.t_weapon_arrow.baseid>== i_dragon_arrow) && !(<findlayer.2.baseid>==i_dragon_bow)
bowcopy i_dragon_bow
emote load dragon bane arrows
return 1

elseif (<findtype.t_weapon_arrow.baseid>== i_silver_arrow) && !(<findlayer.2.baseid>==i_silver_bow)
bowcopy i_silver_bow
emote load silver arrows
return 1

elseif (<findtype.t_weapon_arrow.baseid>== i_speed_arrow) && !(<findlayer.2.baseid>==i_speed_bow)
bowcopy i_speed_bow
emote load speed arrows
return 1

elseif (<findtype.t_weapon_arrow.baseid>== i_arrow) && !(<findlayer.2.baseid>==i_bow)
bowcopy i_bow
emote load normal arrows
return 1
endif

[itemdef bowremover]
id=i_memory

on=@create
attr 02

on=@timer
link.correctammo

//------------------------------------
//             craftable section
//------------------------------------

[itemdef i_barrel_speed]
id=i_keg_small
type=t_normal
name=barrel of speed arrows
weight=5.0
dye=0
value=1000
SKILLMAKE=bowcraft 70.0
resources=40 i_log,20 I_scroll_agility, 40 i_feather

on=@create
color=0791

on=@dclick
if (<topobj.uid>==<src.uid>)
src.newitem i_speed_arrow
src.act.amount 50
src.act.bounce
remove
else
src.message this must be on your person to use it
endif
return 1

[SKILLMENU SM_ARROW_SPECIAL]
special arrows

on=i_barrel_speed <name> (<resmake>)
TESTIF=(<skillclass>==class_ranger)
makeitem=i_barrel_speed

on=i_speed_arrow <name> (<resmake>)
TESTIF=(<skillclass>==class_ranger)
makeitem=i_speed_arrow