////////////////////////////////////////////////////////////////////////////////////
//Graphic Moongate Sytem v1.1
//Scripted by Mordaunt
//Credit to -Ax- who's mapdlg equations were used to plot the moongate on the gump
//Covers Towngates only, and only Trammel plane (see v1.0b for tram/fel)
//
//FIXED Problem with moongate memories not expiring if gump is closed
//TO use replace ALL standard moongates with i_moongate_ss
////////////////////////////////////////////////////////////////////////////////////

[itemdef i_moongate_SS]
NAME=moongate
ID=i_moongate_blue
TYPE=0

ON=@step
IF (<ARGN1>==0)
SRC.NEWITEM i_mem_moongate
SRC.ACT.EQUIP
RETURN 1
ENDIF

[ITEMDEF i_mem_moongate]
ID=i_handr_1
NAME=moongate memory
TYPE=t_EQ_script
LAYER=layer_special

ON=@CREATE
ATTR=attr_decay

ON=@EQUIP
CONT.DIALOG moongatemenu
SFX=snd_spell_gate_travel
TIMER=5

ON=@TIMER
IF <CONT.RESTEST 1 i_mem_moongate>
CONT.CONSUME i_mem_moongate
RETURN 1
ENDIF


ON=@UNEQUIP
CONT.DIALOGCLOSE moongatemenu
RETURN 1

[DIALOG moongatemenu] 
90,90 
resizepic 0 0 5120 403 434 
dtext 20 10 455 Moon Gate Menu: Choose your destination
gumppic 10 29 5528 
button <EVAL (10+((1336*100)/1337))> <EVAL (10+((1997*100)/1067))> 2117 2118 1 0 1  //brit
dtext <EVAL (10+((1336*100)/1337))> <EVAL (((1997*100)/1067)-5)> 80 Britain	
button <EVAL (10+((4467*100)/1337))> <EVAL (10+((1283*100)/1067))> 2117 2118 1 0 2//Moonglow
dtext <EVAL (((4467*100)/1337)-20)> <EVAL (((1283*100)/1067)-5)> 80 Moonglow
button <EVAL (10+((1499*100)/1337))> <EVAL (10+((3771*100)/1067))> 2117 2118 1 0 3//Jhelom
dtext <EVAL (10+((1499*100)/1337))> <EVAL (((3771*100)/1067)-5)> 80 Jhelom
button <EVAL (10+((771*100)/1337))> <EVAL (10+((753*100)/1067))> 2117 2118 1 0 4//Yew
dtext <EVAL (10+((771*100)/1337))> <EVAL (((753*100)/1067)-5)> 80 Yew
button <EVAL (10+((1828*100)/1337))> <EVAL (10+((2948*100)/1067))> 2117 2118 1 0 5//Trinsic
dtext <EVAL (10+((1828*100)/1337))> <EVAL (((2948*100)/1067)-5)> 80 Trinsic
button <EVAL (10+((643*100)/1337))> <EVAL (10+((2067*100)/1067))>  2117 2118 1 0 6//Skara Brae
dtext <EVAL (10+((643*100)/1337))> <EVAL (20+((2067*100)/1067))> 80 Skara Brae
button <EVAL (10+((3564*100)/1337))> <EVAL (10+((2140*100)/1067))> 2117 2118 1 0 7//Magincia
dtext <EVAL (10+((3564*100)/1337))> <EVAL (((2140*100)/1067)-5)> 80 Magincia
button <EVAL (10+((2702*100)/1337))> <EVAL (10+((692*100)/1067))> 2117 2118 1 0 8//Vesper/Minoc
dtext <EVAL (10+((2702*100)/1337))> <EVAL (((692*100)/1067)-5)> 80 Vesper/Minoc
dtext 20 410 455 You are in <REGION.NAME>. 

[DIALOG moongatemenu BUTTON] 
ON=1
SRC.GO 1336,1997,5
SRC.CONSUME i_mem_moongate

ON=2
SRC.GO 4467,1283,5
SRC.CONSUME i_mem_moongate

ON=3
SRC.GO 1499,3771,5
SRC.CONSUME i_mem_moongate

ON=4
SRC.GO 771,753,5
SRC.CONSUME i_mem_moongate

ON=5
SRC.GO 1828,2948,-20
SRC.CONSUME i_mem_moongate

ON=6
SRC.GO 643,2067,5
SRC.CONSUME i_mem_moongate

ON=7
SRC.GO 3564,2140,34
SRC.CONSUME i_mem_moongate

ON=8
SRC.GO 2702,692,5
SRC.CONSUME i_mem_moongate